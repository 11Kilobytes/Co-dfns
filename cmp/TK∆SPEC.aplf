 TK∆SPEC
⍝ Stimuli
⍝  AL: Alphabetic Character
⍝  CO: Colon
⍝  ET: End of input
⍝  NL: CR or LF
⍝  OT: Other character
⍝  WS: Whitespace -- Tab, space 
⍝ Responses
⍝  ER: Error
⍝  ET: End of Stream
⍝  KW: Keyword Begins
⍝  NL: Newline/Statement Separator
⍝  RW: Rewind reader
⍝  ST: Store Data
⍝ Parameters
⍝  READ: Get the next event/token in the sequence
⍝  SEQ:  Set to the current sequence state we are in
∆:SEQ←'∊' ⋄ →⎕LC+READ
_AL: ER   ⋄ →0
_CO:      ⋄ →CO∆
_ET: ET   ⋄ →0
_NL: NL   ⋄ →0
_OT: ER   ⋄ →0
_WS:      ⋄ →∆
 SEQ⎕SIGNAL 99

CO∆:SEQ←'CO' ⋄ →⎕LC+READ
CO_AL: ST    ⋄ →CO_AL∆
CO_CO: ER    ⋄ →0
CO_ET: ER    ⋄ →0
CO_NL: ER    ⋄ →0
CO_OT: ER    ⋄ →0
CO_WS: ER    ⋄ →0
 SEQ⎕SIGNAL 99

CO_AL∆:SEQ←'CO AL' ⋄ →⎕LC+READ
CO_AL_AL: ST       ⋄ →CO_AL∆
CO_AL_CO: KW ⋄ RW  ⋄ →0
CO_AL_ET: KW       ⋄ →0
CO_AL_NL: KW ⋄ RW  ⋄ →0
CO_AL_OT: ER       ⋄ →0
CO_AL_WS: KW       ⋄ →0
 SEQ⎕SIGNAL 99
