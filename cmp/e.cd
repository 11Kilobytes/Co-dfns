⍝ A  B  E  F  G  L  M  N  O  P  V  Z
⍝ 0  1  2  3  4  5  6  7  8  9 10 11
tt←{⍞←'C' ⋄ ((d t k n)exp sym)←⍵ ⋄ I←{(⊂⍵)⌷⍺}

 ⍝ Convert to Parent Vector, record scopes
 _←2{l[⍵[i]]←⍵[¯1+i←⍸0,2=⌿i]⊣p[⍵]←⍺[i←⍺⍸⍵]}⌿⊢∘⊂⌸d⊣p←l←⍳≢d

 ⍝ Function Roots and Exports
 rf←I@{t[⍵]≠3}⍣≡⍨p ⋄ exp←(rf[i])(n[i])(t[i])(i←⍸(t=1)∧p[rf]=rf)

 ⍝ Lift Functions
 i←⍸(t=3)∧p≠⍳s←≢p ⋄ l←i(s+⍳)@{⍵∊i}l ⋄ l[j]←⊃(⌽i),j←⍸(p=⍳s)∧l=⍳s
 p l t k n,←(p[i])(l[i])10 1 i⍴⍨¨≢i ⋄ p[i]←i ⋄ l[i]←(≢i)↑(⊃i),i

 ⍝ Wrap Statements
 i←⍸(t∊0 1 2 10)∧(t[p]=3)∨(t[p]=4)∧(0@(l⌿⍨l≠⍳s)⊢1⍴⍨s←≢l) ⋄ l←i(s+⍳)@{⍵∊i}l
 p l t k n,←(p[i])(l[i])2(-(t[i]=1)∧t[p[i]]=3)0⍴⍨¨≢i ⋄ p[i]←s+⍳≢i ⋄ l[i]←i

 ⍝ Lift Guard Tests
 gi←p[i←⍸(t∊10,⍳3)∧(l=⍳≢l)∧t[p]=4] ⋄ l[j]←j←⍸l∊i
 l[i]←gl[j]@(j←⍸gi≠gl←l[gi])⊢i ⋄ l[gi]←i ⋄ n[gi]←i ⋄ p[i]←p[gi]

 ⍝ Lift Expressions
 _←{n[⍺]←⍵}⌿↓⍉p[i],∘≢⌸i←⍸(t[p]=2)∧k[p]=3
 i←⍸(t∊(⍳3),8+⍳3)∧m←~t[p]∊3 4 ⋄ xw←x@(l[x])⊢x@(x←⍸m)⊢l ⋄ x←i⌿⍨i=l[i]
 l[p[x]]←x ⋄ l[j]←xw I p I@(xw∘I=⊢)⍣≡j←i~p[i] ⋄ p[i]←p I@{~t[⍵]∊3 4}⍣≡p[i]
 l[x]←x←j⌿⍨p[l[j]]≠p[j]

 ⍝ Compute Frames and Slots
 sl←¯1,⍨∊⍳¨n[∪rb]←⊢∘≢⌸rb←0⌷⍉fe←∪I∘⍋⍨rn←rf[bi],⍪n[bi←⍸t=1]
 d[i←⍸t=3]←0 ⋄ _←{z⊣d[i]+←⍵≠z←rf[⍵]}⍣≡i ⋄ fr←d[fe[;0]],¯1 ⋄ d←(≢p)↑d

 ⍝ Anchor Variables to Frame and Slot
 vi←⍸(t=10)∧n<¯4 ⋄ x←n[y←vi,bi] ⋄ n[bi]←sl[fe⍳rn] ⋄ rf,←p I@{t[⍵]≠3}⍣≡p↓⍨≢rf
 fs←(≢x)⍴c←≢fe ⋄ _←{z/⍨c=fs[1⌷z]←fe⍳⍉x I@1⊢z←rf I@0⊢⍵}⍣≡(vi,rf[bi])⍪⍉⍪⍳≢x
 fr sl←(fr sl I¨⊂fs)⊣@y¨⊂(≢rf)⍴¯1
                                
 ⍝ Inline Functions
 ⍝ Propagate constants
 ⍝ Fold constants
 ⍝ Dead, useless code elimination
 ⍝ Allocate frames

 p l t k n fr sl rf d exp sym}
