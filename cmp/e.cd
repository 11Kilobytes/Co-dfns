⍝ A  B  E  F  G  L  M  N  O  P  V  Z
⍝ 0  1  2  3  4  5  6  7  8  9 10 11
tt←{⍞←'C' ⋄ ((d t k n)exp sym)←⍵ ⋄ I←{(⊂⍵)⌷⍺}

 ⍝ Convert to Parent Vector, record scopes
 _←2{l[⍵[i]]←⍵[¯1+i←⍸0,2=⌿i]⊣p[⍵]←⍺[i←⍺⍸⍵]}⌿⊢∘⊂⌸d⊣p←l←⍳≢d
 rf←I@{t[⍵]≠3}⍣≡⍨p

 ⍝ Lift Functions
 i←⍸(t=3)∧p≠⍳s←≢p ⋄ l←i(s+⍳)@{⍵∊i}l ⋄ l[j]←⊃(⌽i),j←⍸(p=⍳s)∧l=⍳s
 p l t k n,←(p[i])(l[i])10 1 i⍴⍨¨≢i ⋄ p[i]←i ⋄ l[i]←(≢i)↑(⊃i),i

 ⍝ Wrap Return Expressions
 i←⍸((t∊0 2 10)∧t[p]=3)∨(t[p]∊3 4)∧(0@(l⌿⍨l≠⍳s)⊢1⍴⍨s←≢l)∧(t∊0 2 10)∨(t=1)∧k=0
 l←i(s+⍳)@{⍵∊i}l ⋄ p l t k n,←(p[i])(l[i])2 0 0⍴⍨¨≢i ⋄ p[i]←s+⍳≢i ⋄ l[i]←i

 ⍝ Lift Guard Tests
 gi←p[i←⍸(t∊10,⍳3)∧(l=⍳≢l)∧t[p]=4] ⋄ l[j]←j←⍸l∊i
 l[i]←gl[j]@(j←⍸gi≠gl←l[gi])⊢i ⋄ l[gi]←i ⋄ n[gi]←i ⋄ p[i]←p[gi]

 ⍝ Lift Expressions (Shift to RPN)
 i←⍸(t∊(⍳3),8+⍳3)∧m←~t[p]∊3 4 ⋄ xw←x@(l[x])⊢x@(x←⍸m)⊢l ⋄ x←i⌿⍨i=l[i]
 l[p[x]]←x ⋄ l[j]←xw I p I@(xw∘I=⊢)⍣≡j←i~p[i] ⋄ p[i]←p I@{~t[⍵]∊3 4}⍣≡p[i]
 l[x]←x←j⌿⍨p[l[j]]≠p[j]

 ⍝ Anchor Variables to Frame and Slot
 sl←¯1,⍨(⍳∘≢-+⍀I⍨0,⍸)0,2≠⌿0⌷⍉fe←∪I∘⍋⍨rf[i],⍪n[i←⍸t=1] ⋄ vi←⍸t=10 ⋄ bi←⍸t=1
 d[i←⍸t=3]←0 ⋄ _←{z⊣d[i]+←⍵≠z←rf[⍵]}⍣≡i ⋄ fr←d[fe[;0]],¯1 ⋄ fs←(+⌿≢¨vi bi)⍴≢fe
 x←n[y←vi,bi] ⋄ rf,←p I@{t[⍵]≠3}⍣≡p↓⍨≢rf ⋄ _←(⍪vi,rf[bi])(⍪⍳≢fs)
 f s←(fr sl I¨⊂fs)⊣@y¨⊂(≢rf)⍴¯1⊣{⍵ I¨⊂⍸(≢fe)=fs[⊃⌽⍵]←fe⍳⊃rf x,.I ⍵}⍣≡_

 ⍝ Inline Functions
 ⍝ Propagate constants
 ⍝ Fold constants
 ⍝ Dead, useless code elimination
 ⍝ Allocate frames

 p l t k n f s exp sym}
