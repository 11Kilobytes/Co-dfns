f∆ N∆←'ptknfsrdx' 'ABEFGLMNOPVZ'
⎕FX∘⍉∘⍪¨'GLM',¨'←{⍪/(0 '∘,¨(⍕¨N∆⍳'GLM'),¨⊂' 0 0),1+@0⍉↑(⊂4⍴⊂⍬),⍵}'
⎕FX∘⍉∘⍪¨'ABEFO',¨'←{⍺←0 ⋄ ⍪/(0 '∘,¨(⍕¨N∆⍳'ABEFO'),¨⊂' ⍺⍺ ⍺),1+@0⍉↑(⊂4⍴⊂⍬),⍵}'
⎕FX∘⍉∘⍪¨'NPVZ',¨'←{0(N∆⍳'''∘,¨'NPVZ',¨''')'∘,¨'0(⍎⍵)' '⍺⍺(⊂⍵)' '⍺⍺(⊂⍵)' '1(⊂⍵)',¨'}'
Vt←(⊢⍳⍨0⊃⊣)⊃¯1,⍨1⊃⊣
MkAtom←{∧⌿m←(N∆⍳'N')=⊃¨1⊃¨⍵:0 A⌽⍵ ⋄ 1=≢⍵:0⊃⍵ ⋄ 3 A⌽0 A∘⊂¨@{m}⍵}
MkBfn←{0(N∆⍳'F')¯1(,⊂⌽1↓¯1↓⍵)}
MkMget←{⍪/(0,1+2<≢⊃z)+@0⊢z←⍉↑⌽⍵}
Fn←{0=≢⍵:0 ⍬ ⍺ ''
 0=≢ns←(3⊃z)⌿⍨m←((3=1⊃⊢)∧¯1=2⊃⊢)⊢z←⍪⌿↑⍵:0(,⊂z)⍺ ''
 0<c←⌈⌿⊃r←↓⍉↑⍺∘Fa¨ns:c ⍬ ⍺ ⍵
 0 (,⊂(⊂¨¨z)((⊃⍪⌿)⊣@{m})¨⍨↓(m⌿0⊃z)+@0⍉↑⊃¨1⊃r) ⍺ ''}
FnType←3 3 2 2⊥1+(⊂⊃⍳(,¨'⍵⍵' '⍺⍺','⍺⍵')⍨)⌷1∘⊃,¯1⍨
PEG'Sfn    ← sfn                                              : 1P∘⌽∘∊       '
PEG'Prim   ← prim                                             : 1P           '
PEG'Symbol ← name                                             : ⌽            '
PEG'Name   ← Symbol & (⍺⍺=Vt)                                 : ⍺⍺ V∘,∘⊃     '
PEG'Args   ← aaww | aw & (⍺⍺=Vt)                              : ⍺⍺ V∘,∘⊃     '
PEG'Var    ← ⍺⍺ Args | (⍺⍺ Name)                                             '
PEG'Num    ← float | int                                      : N∘⌽          '
PEG'Pex    ← rpar , Ex , lpar                                                '
PEG'Zil    ← zil                                              : 0A           '
PEG'Unit   ← (0 Var) | Num | Zil | Pex                                       '
PEG'Atom   ← Unit+                                            : MkAtom       '
PEG'Semi   ← ∊                                                : 0 P{,'';''}  '
PEG'Semx   ← Ex | Semi                                                       '
PEG'Brk    ← rbrk , (Semx , (semi , Semx *)) , lbrk           : 3E∘⌽         '
PEG'Lbrk   ← ∊                                                : ⍺⍺ P{,''[''} '
PEG'Idx    ← Brk , (1 Lbrk) , Atom                            : 2E∘⌽         '
PEG'Blrp   ← ⍺⍺ , (⍵⍵ Slrp ∇)                                                '
PEG'Slrp   ← ⍺⍺ | (⍵⍵ , ∇) | (⍥ , ∇)                                         '
PEG'Pfe    ← rpar , Fex , lpar                                               '
PEG'Bfn    ← rbrc Blrp lbrc                                   : MkBfn        '
PEG'Fnp    ← Prim | (1 Var) | Sfn | Bfn | Pfe                                '
PEG'Pmop   ← mop                                              : 2P           '
PEG'Mop    ← Pmop , Afx                                       : 2O           '
PEG'Pdop1  ← dop1                                             : 2P           '
PEG'Dop1   ← Pdop1 , Afx                                      : 8O∘⌽         '
PEG'Pdop2  ← dop2                                             : 2P           '
PEG'Vop    ← Atom , Pdop2 , Afx                               : 7O∘⌽         '
PEG'Pdop3  ← dop3                                             : 2P           '
PEG'JotDot ← dot , jot                                        : 2O∘,∘⊂∘(2P)∘⌽'
PEG'Dop3a  ← Pdop3 , Atom                                     : 5O∘⌽         '
PEG'Dop3   ← Dop3a | JotDot                                                  '
PEG'Bop    ← rbrk , Ex , lbrk , (2 Lbrk) , Afx                : 7O∘⌽         '
PEG'Fop    ← Fnp , (Dop1 | Dop3 ?)                            : ⍪/⍳∘≢+@0⍉∘↑∘⌽'
PEG'Afx    ← Mop | Fop | Vop | Bop                                           '
PEG'Trn    ← Afx , (Afx | Idx | Atom , (∇ ?) ?)               : 3F∘⌽         '
PEG'Bind   ← gets , Symbol [⍺⍺]                               : ⍺⍺ B∘⍬       '
PEG'Mname  ← 0 Name                                           : 0 3∘⊃4E⊢     '
PEG'Gets   ← ∊                                                : ⍺⍺ P{,''←''} '
PEG'Ogets  ← 2 Gets                                           : 2O∘⌽         '
PEG'Mbrk   ← Ogets , Brk , (0 Name)                           : 2 3∘⊃4E∘⌽2↑⊢ '
PEG'Mget   ← Afx , (Mname | Mbrk)                             : MkMget       '
PEG'Bget   ← 1 Gets , Brk , (0 Name)                          : 2 3∘⊃4E∘⌽2↑⊢ '
PEG'Asgn   ← gets , (Bget | Mget)                                            '
PEG'Fex    ← Afx , (Trn ?) , (1 Bind *)                       : ⍪/⍳∘≢+@0⍉∘↑∘⌽'
PEG'IAx    ← Idx | Atom , (dop2 !)                                           '
PEG'App    ← Afx , (IAx ?)                                    : {(≢⍵)E⌽⍵}    '
PEG'ExHd   ← Asgn | (0 Bind) | App , ∇ ?                                     '
PEG'Ex     ← IAx , ExHd                                       : ⍪/⍳∘≢+@0⍉∘↑∘⌽'
PEG'Gex    ← Ex , grd , Ex                                    : G∘⌽          '
PEG'Nlrp   ← sep | eot Slrp (lbrc Blrp rbrc)                                 '
PEG'Stmts  ← sep* , (Nlrp → (⍺⍺ , eot∘⌽))* , eot                             '
PEG'Alp    ← ∊                                                : ''⍺''⍨       '
PEG'Omg    ← ∊                                                : ''⍵''⍨       '
PEG'ClrEnv ← (Alp[¯1]),(Alp,Alp[¯1]),(Omg[¯1]),(Omg,Omg[¯1])↓                '
PEG'Fax    ← Gex | Ex | Fex Stmts → Fn                        : (FnType ⍺)F  '
PEG'FaFnW  ← Omg[0]↓ , Fax []                                                '
PEG'FaFnA  ← Omg[0] , (Alp[0])↓ , Fax []                                     '
PEG'FaFn   ← FaFnW | FaFnA                                                   '
PEG'FaMopV ← Alp,Alp[0]↓ , FaFn []                                           '
PEG'FaMopF ← Alp,Alp[1]↓ , FaFn []                                           '
PEG'FaMop  ← FaMopV , (FaMopF ?) | FaMopF                                    '
PEG'FaDopV ← Omg,Omg[0]↓ , FaMop []                                          '
PEG'FaDopF ← Omg,Omg[1]↓ , FaMop []                                          '
PEG'FaDop  ← FaDopV , (FaDopF ?) | FaDopF                                    '
PEG'Fa     ← ClrEnv , (FaFn | FaMop | FaDop) []                              '
PEG'Ns     ← nss Blrp nse → (Ex | Fex Stmts → Fn) , eot       : 0F           '
ps←{⍞←'P' ⋄ 0≠⊃c a e r←⍬ ⍬ Ns∊{⍵/⍨∧\'⍝'≠⍵}¨⍵,¨⎕UCS 10:⎕SIGNAL c
 (↓s(-⍳)@3↑⊃a)e(s←∪0(,'⍵')(,'⍺')'⍺⍺' '⍵⍵',3⊃⊃a)}
