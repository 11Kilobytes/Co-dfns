 Z←PX∆READ;t;v;p;iDF;iGT;iNM;oBD;oBT;oDF;oET;oVU
⍝ PARAMETERS
⍝  xt:    Expression token stream, token type
⍝  xv:    Expression token stream, token value
⍝  xp:    Expression token stream, token position
⍝  enm:   Environment variable name
⍝  etp:   Environment variable type
⍝  idx:   Current position in the token stream
⍝  DEBUG: Boolean, enable debugging or not
⍝  ISDFN: Boolean, are we tokenizing the body of a dfn?
⍝  SEQ:   Current sequence history
⍝  ER:    ERROR
 iDF iGT iNM←⍳3
 oBD oBT oDF oET oVU←1+⍳5
 →START⌿⍨0=⎕NC'DEBUG'
 ⍎DEBUG⌿'⎕←''PX: '',SEQ'
START:idx+←1
 Z←oET⌿⍨f←idx≥≢xt          ⋄ →f⌿0
 t←idx⊃xt ⋄ v←idx⊃xv
 Z←oBD⌿⍨f←ISDFN∧t=iGT      ⋄ →f⌿0
 Z←oBT⌿⍨f←(~ISDFN)∧t=iGT   ⋄ →f⌿0
 Z←oDF⌿⍨f←t=iDF            ⋄ →f⌿0
 Z←oVU⌿⍨f←(t=iNM)∧~enm∊⍨⊂v ⋄ →f⌿0
 SEQ,←' OT' ⋄ ER
