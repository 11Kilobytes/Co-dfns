TT←{
	((d t k n lx ss se)exp sym src)←⍵

	⍝ Compute parent vector and reference scope
	r←I@{t[⍵]≠F}⍣≡⍨p⊣2{p[⍵]←⍺[⍺⍸⍵]}⌿⊢∘⊂⌸d⊣p←⍳≢d

	⍝ Lift dfns to the top-level
	p,←n[i]←(≢p)+⍳≢i←⍸(t=F)∧p≠⍳≢p ⋄ t k n lx ss se r(⊣,I)←⊂i
	p r I⍨←⊂n[i]@i⊢⍳≢p ⋄ t[i]←C

	⍝ Wrap expressions as binding or return statements
	i←(⍸(~t∊F G)∧t[p]=F),i⌿⍨2|⍳≢i←i[⍋p[i←⍸t[p]=G]]
	p t k n lx ss se r⌿⍨←⊂m←2@i⊢1⍴⍨≢p
	p r i I⍨←⊂j←(+⍀m)-1 ⋄ n←j I@(0≤⊢)n ⋄ p[i]←j←i-1
	k[j]←-(k[r[j]]=0)∨0@({⊃⌽⍵}⌸p[j])⊢(t[j]=B)∨(t[j]=E)∧k[j]=4
	t[j]←E

	⍝ Lift guard tests
	p[i]←p[x←¯1+i←i⌿⍨~2|⍳≢i←i[⍋p[i←⍸t[p]=G]]]
	t[i,x]←t[x,i] ⋄ k[i,x]←k[x,i] ⋄ n[x]←n[i] ⋄ lx[x,i]←lx[i,x]
	ss[x,i]←ss[i,x] ⋄ se[x,i]←se[i,x]
	p←((x,i)@(i,x)⊢⍳≢p)[p]

	⍝ Lift and flatten expressions
	j←(≢p)+⍳≢i←⍸(~t[p]∊F G)∧t∊A B C E O
	p←(j@i⊢⍳≢p)[p] ⋄ p t k n lx r ss se(⊣,I)←⊂i ⋄ t k n(⊣@i⍨)←V 0 j
	p[i]←p[x←p I@{~t[p[⍵]]∊F G}⍣≡i←⍸t∊G A B C E O] ⋄ j←(⌽i)[⍋⌽x]
	p t k n lx ss se r{⍺[⍵]@i⊢⍺}←⊂j ⋄ p←(i@j⊢⍳≢p)[p]

	⍝ Namify pointer variables
	i←⍸n≥0 ⋄ sym∪←x←('ptr',⍕)¨n[i] ⋄ n[i]←-sym⍳x

	⍝ Symbol mapping between primitives and runtime names
	syms ←,¨'+'   '-'   '×'   '÷'   '*'   '⍟'   '|'   '○'   '⌊'  
	nams ←  'add' 'sub' 'mul' 'div' 'exp' 'log' 'res' 'cir' 'min'
	syms,←,¨'⌈'   '!'   '<'   '≤'   '='   '≥'   '>'   '≠'   '~'
	nams,←  'max' 'fac' 'lth' 'lte' 'eql' 'gte' 'gth' 'neq' 'not'
	syms,←,¨'∧'   '∨'   '⍲'   '⍱'   '⌷'   '['   '⍳'   '⍴'   ','  
	nams,←  'and' 'lor' 'nan' 'nor' 'sqd' 'brk' 'iot' 'rho' 'cat' 
	syms,←,¨'⍪'   '⌽'   '⍉'   '⊖'   '∊'   '⊃'   '≡'   '≢'   '⊢'
	nams,←  'ctf' 'rot' 'trn' 'rtf' 'mem' 'dis' 'eqv' 'nqv' 'rgt'
	syms,←,¨'⊣'   '⊤'   '⊥'   '/'   '⌿'   '\'   '⍀'   '?'   '↑'   
	nams,←  'lft' 'enc' 'dec' 'red' 'rdf' 'scn' 'scf' 'rol' 'tke' 
	syms,←,¨'↓'   '¨'   '⍨'   '.'   '⍤'   '⍣'   '∘'   '∪'   '∩'
	nams,←  'drp' 'map' 'com' 'dot' 'rnk' 'pow' 'jot' 'unq' 'int'
	syms,←,¨'←'   '⍋'   '⍒'   '∘.'  '⍷'   '⊂'   '⌹'   '⊆'   '∇'
	nams,←  'get' 'gdu' 'gdd' 'oup' 'fnd' 'par' 'mdv' 'nst' 'del'
	syms,←,¨';'   '⎕FFT' '⎕IFFT' '⎕CONV' 
	nams,←  'spn' 'fft'  'ift'   'cnv'
	syms,←,¨'⍺'     '⍵'     '%u'
	nams,←  'alpha' 'omega' ''
	
	⍝ Convert all primitives to variables; P → V
	i←⍸t=P ⋄ t[i]←V ⋄ sym[i]←nams[syms⍳sym[i←|n[i]]]

	p t k n lx ss se sym
}
