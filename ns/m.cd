Fix←{n LK⊃Init CL (VF ⍺)WM GC CP FD AV FE LF RF LC DF DU DL⊃a n←PS TK VI ⍵⊣FI}
Init←{⍵(I 5⍴0)⊣'I'⎕NA'I4 ',⍵,'|Init P P P P I'}
VI←{~1≡≢⍴⍵:E 11 ⋄ 2≠|≡⍵:E 11 ⋄ ~∧/1≥≢∘⍴¨⍵:E 11 ⋄ ~∧/∊' '=(⊃0⍴⊂)¨⍵:E 11 ⋄ ⍵}
VF←{~(∧/∊' '=⊃0⍴⊂⍵)∧(1≡≢⍴⍵)∧(1≡≡⍵):E 11 ⋄ ⍵}
CC←'nvcc -O3 -g -shared -Xcompiler -fPIC -Irt -Xlinker -L. -o '
CL←{⍵,'.so'⊣⎕SH CC,'"',⍵,'.so" "',⍵,'.c" -lcodfns'}
LK←{⊃v(⍵{⍵⊣⍵.⍎⍺,'←''',⍺,'''##.NA''',⍺⍺,''' ⋄ 0'})¨⍣(0≠≢v←⊣/⍺⌿⍨2=⊢/⍺)⊂⎕NS⍬}
WM←{⍺⊣(⊃,/,⍵,⎕UCS 10)put⍺,'.c'}
NA←{⍺←⊢ ⋄ fmt←⍺⍺{'I4 ',⍵⍵,'|',(⍺⍺,⍵),' P P P P I'}⍵⍵ ⋄ f←{0=⎕NC'⍺':fm ⍵ ⋄ fd ⍵}
 _←'fm'⎕NA fmt'm' ⋄ _←'fd'⎕NA fmt'd' ⋄ 0≠⊃e o←EA⍬:E e ⋄ 0≠⊃e w←AP ⍵:E e
 0≠⊃e a←AP ⍺⊣⍬:E e ⋄ 0≠e←⍺ f o a w 0 WithGPU:E e ⋄ t z←ConvertArray o
 _←array_free¨o a w ⋄ _←free¨o a w ⋄ t{0≠⍺:⍵}z}
EA←{#.Codfns.ffi_make_array_double 1 0 0 ⍬ ⍬}
AP←{1 3∨.=10|⎕DR ⍵:ffi_make_array_int 1(≢⍴⍵)(≢,⍵)(⍴⍵)(,⍵)
 5∨.=10|⎕DR ⍵:ffi_make_array_double 1(≢⍴⍵)(≢,⍵)(⍴⍵)(,⍵)
 E 99}
VarType←{(⍺[;1],0)[⍺[;0]⍳⊂⍵]}
Split←{' '((≠(/∘⊢)(1,(1↓(¯1⌽=))))⊂(≠(/∘⊢)⊢))⍵}
Sel←{~∨/⍺:⍵ ⋄ g←⍵⍵⍣(⊢/⍺) ⋄ 2=⍴⍺:⍺⍺⍣(⊣/⍺)g ⍵ ⋄ (¯1↓⍺)⍺⍺ g ⍵}
Prop←{(¯1⌽P∊⊂⍺)/P←(⊂''),,↑⍵[;3]}
Kids←{((⍺+⊃⍵)=0⌷⍉⍵)⊂[0]⍵}
GEPI←{{ConstInt(Int32Type)⍵ 0}¨⍵}
Eachk←{(1↑⍵)⍪⊃⍪/(⊂MtAST),(+\(⊃=⊢)0⌷⍉k)(⊂⍺⍺)⌸k←1↓⍵}
Comment←{⍺}
ByElem←{(⍺[;1]∊⊂⍵)⌿⍺}
ByDepth←{(⍵=⍺[;0])⌿⍺}
Bind←{0=≢⍵:⍵ ⋄ (i←A⍳⊂'name')≥⍴A←0⌷⍉⊃0 3⌷Ast←⍵:Ast⊣(⊃0 3⌷Ast)⍪←'name'⍺
 Ast⊣((0 3)(i 1)⊃Ast){⍺,⍵,⍨' ' ''⊃⍨0=⍴⍺}←⍺}
E←{⍺←⊢ ⋄ ⍺ ⎕SIGNAL ⍵}
put←{tie←{0::⎕SIGNAL ⎕EN ⋄ 22::⍵ ⎕NCREATE 0 ⋄ 0 ⎕NRESIZE ⍵ ⎕NTIE 0}⍵
 size←(¯128+256|128+'UTF-8'⎕UCS ⍺)⎕NAPPEND tie 83 ⋄ 1:rslt←size⊣⎕NUNTIE tie}


