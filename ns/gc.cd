⍝ Generate C code
GC←{z←,⊂'#include "codfns.h"'
 di←¯1 ⋄ md←{'D',⍕di⊣(⊃di)+←1} ⋄ si←¯1 ⋄ ms←{'S',⍕si⊣(⊃si)+←1}
 ce←(tm/1⌽(1⌷⍉⍵)∊⊂'Number')/t←(tm←1=0⌷⍉⍵)⊂[0]⍵
 ntyp←{('int' 'float'⍳⊂⊃'class'Prop 1↓⍵)⊃'type_i' 'type_d' 'type_na'}
 nenm←{'apl_',ntyp⍵} ⋄ gshp←{'uint64_t ',⍺,'[]={',((1=z)⊃(⍕z←gsz⍵)''),'};'}
 gdat←{(ntyp⍵),' ',⍺,'[]={',(com('value'Prop 1↓⍵),⊂''),'};'}
 com←{⊃{⍺,',',⍵}/⍵} ⋄ gnm←{⊃'name'Prop 1↑⍵} ⋄ gsz←{+/(1⌷⍉⍵)∊⊂'Number'}
 lit←{'struct codfns_array ',(gnm⍵),'={',(com(⍕1≠gsz⍵)(⍕gsz⍵)(nenm⍵),⍺),'};'}
 z,←⊃,/(⊂0⍴⊂''),{s←ms⍬ ⋄ d←md⍬ ⋄ (s gshp⍵)(d gdat⍵)(s d lit⍵)}¨ce
 gbl←{⊂'struct codfns_array ',(com gnm¨⍺),';'} ⋄ ve←(tm/1⌽(1⌷⍉⍵)∊⊂'Variable')/t
 z,←ve gbl⍣(0≠≢ve)⊢⍬ ⋄ z,←⊂'int Init(struct codfns_array res,'
 z,←⊂' struct codfns_array lft,struct codfns_array rgt){'
 z,←{' array_cp(',(gnm⍵),',',(gnm 1↓⍵),');'}¨ve ⋄ z,←⊂' return 0;}'
 gfh←{z←'int ',(gnm⍵),'(struct codfns_array res,' 
  z' struct codfns_array lft,struct codfns_array rgt){'}
 gf←{(gfh⍵),(⊂' array_mt(res);'),(⊂' return 0;}')}
 z,←⊃,/(⊂0⍴⊂''),gf¨fn←(tm/1⌽(1⌷⍉⍵)∊⊂'Function')/t
 
 ⍪z}
