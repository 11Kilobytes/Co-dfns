\input ConTeXtLPMacros

\definefontsynonym[APL][Apl385]
\setupbodyfont[xits]
\definefont[tt][APL sa 1]

\setuppapersize[letter][letter]
\setupwhitespace[medium]

\starttext
\startfrontmatter
\title{HPAPL: The Compiler}

\completecontent
\vfill
Copyright $\copyright$ 2012 Aaron W. Hsu $⟨${\tt arcfide@sacrideo.us}$⟩$

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
\stopfrontmatter

\startbodymatter
\chapter{Introduction}

This is a first simple attempt to get a small piece of each element of 
the HPAPL compiler working and running.

\defchunk{HPAPL Namespace}
:Namespace HPAPL
  ⎕IO ⎕ML←0 0
  /BTEX\chunk{HPAPL Code}/ETEX
  ∇ Reload;N
    'HPAPL Namespace' #.ConTeXtLP.Tangle './hpapl.tex' './hpapl.dyalog'
    N←#.⎕SE.SALT.Load './hpapl -Target=#'
    ⎕←'Loaded: ',⍕N
  ∇
:EndNamespace
\stopchunk


\chapter{Parsing and Pretty Printing}

This chapter focuses on getting the HPAPL program from strings into 
a reasonable representation, and getting back into a string again. Here 
is the grammar of our language:\index{Language Grammar}

\define[1]\term{\hbox{\tt #1}}

\startformula
\eqalign{
  Statements &\Rightarrow Assignment Statements\ |\ \epsilon
  Assignment &\Rightarrow Variable\ \term{←}\ VarOrNum\cr
  VarOrNum   &\Rightarrow Variable\ |\ Number\cr
  Variable   &\Rightarrow \hbox{\tt variable}\cr
  Number     &\Rightarrow \hbox{\tt number}\cr
}
\stopformula

Let's start with a tokenizer.

\defchunk{HPAPL Code}
TokPats←'[0-9]+' '[a-zA-Z][a-zA-Z0-9]*' '←'
TokTypes←'Number' 'Variable' 'Assignment'
Tokenize←{
  tie←⍵ ⎕NTIE 0
  MakeTok←{⍵.PatternNum,⊂⍵.Block[(⊃⍵.Offsets)+⍳⊃⍵.Lengths]}
  (⎕NUNTIE tie)⊢(TokPats⎕S MakeTok) tie
}
\stopchunk

Now let's move to parsing.  We use a parser combinator style of parser. 
In the first place, we need to have a simple way to parse a specific 
token and create an entry in our syntax tree when we 
parse.  Our syntax tree is a three-column matrix where the first 
column is the nesting level, the second column is the node name or type, 
and the third is a vector of values or fields for that node.

\defchunk{HPAPL Code}
ParseTok←{
  ⍵⍵=⊃⊃⍵: (⊂1 3⍴⍺,(⊂⍺⍺),⊂1⊃⊃⍵),⊂1↓⍵
  ⎕SIGNAL 2
}
ParseNum←('Number' ParseTok 0)
ParseVar←('Variable' ParseTok 1)
ParseAssgnTok←('AssignTok' ParseTok 2)
\stopchunk

Now that we have basic parsers for the tokens to normal AST nodes, 
it's important to be able to parse a sequence of these.

\defchunk{HPAPL Code}
ParseSeq←{
  ⎕←'Parsing sequence'
  n1 r1←⍺ ⍺⍺ ⍵
  ⎕←'First'
  ⎕←n1
  n2 r2←⍺ ⍵⍵ r1
  ⎕←'Second'
  ⎕←n2
  (⊂n1⍪n2),⊂r2
}
ParseAssgn←{
  n r←(1+⍺)(ParseVar ParseSeq ParseAssgnTok ParseSeq ParseVarOrNum) ⍵
  (⊂(⍺,(⊂'Assignment'),⊂⍬)⍪n[0 2;]),⊂r
}
\stopchunk

The \cid{ParseVarOrNum} parser deals with a choice, which we write down 
here.

\defchunk{HPAPL Code}
ParseChoice←{
  2:: ⍺ ⍵⍵ ⍵
  ⍺ ⍺⍺ ⍵
}
ParseVarOrNum←(ParseVar ParseChoice ParseNum)
\stopchunk

To extend this out to handle a series of assignment statements, let's 
add the ability to parse $\epsilon$. 

\defchunk{HPAPL Code}
ParseEmpty←{
  0=⍴⍵: 0 3⍴⍬
  ⎕SIGNAL 2
}
\stopchunk

And now let's define our statments non-terminal.

\defchunk{HPAPL Code}
ParseStmts←{⍺ (ParseAssgn ParseSeq ParseStmts ParseChoice ParseEmpty) ⍵}
\stopchunk


\chapter{Outputting C Code}

Let's take our basic syntax tree and print out a C version of the 
program.

\defchunk{HPAPL Code}
∇ FN OutputC AST;tie
  tie←OpenFile FN
  tie println '#include <stdio.h>'
  tie println '#include <stdlib.h>'
  tie println ''
  tie println 'int main(int argc, char *argv[])'
  tie println '{'
  tie print   '  '
  tie OutputCAssgn AST
  tie println '  return 0;'
  tie println '}'
  ⎕NUNTIE tie
∇
∇ TIE OutputCAssgn AST
  TIE OutputCVar AST ChildFields 'Variable'
  TIE print ' = '
  TIE OutputCNum AST ChildFields 'Number'
  TIE println ';'
∇
OutputCNum←{⍺ print 2⊃⍵}
OutputCVar←{⍺ print 2⊃⍵}
\stopchunk

We get the child fields of the current head of the tree using the following.

\defchunk{HPAPL Code}
ChildFields←{
  cur←⊃0⌷⍺
  bv←0,~∨\cur=1↓0⌷[1]⍺
  kids←bv⌿⍺
  ((1⌷[1]kids)⍳⊂⍵)⌷kids
}
\stopchunk


We use the following utilities for the above.

\defchunk{HPAPL Code}
OpenFile←{
  22:: ⍵ ⎕NCREATE 0
  tie←⍵ ⎕NTIE 0
  _←⍵ ⎕NERASE tie
  ⍵ ⎕NCREATE 0
}
∇ TIE println DATA
  TIE print DATA
  (⎕UCS 10)⎕NAPPEND TIE,80
∇
print←{(⎕UCS 'UTF-8' ⎕UCS ⍵)⎕NAPPEND ⍺,80}
\stopchunk

\chapter{Compiler}

Let's use the following procedure to do the basic compilation.

\defchunk{HPAPL Code}
Compile←{
  toks←Tokenize ⍺
  ast rest←0 ParseAssgn toks
  ⍵ OutputC ast
}
\stopchunk


\stopbodymatter

\startappendices

\chapter[designpatterns]{Design Patterns}

\section[visitorpattern]{Visitor Pattern}

\section[factorypattern]{Factory Pattern}

\stopappendices

\startbackmatter
\completeindex
\stopbackmatter

\stoptext
