\input ConTeXtLPMacros

\definefontsynonym[APL][Apl385]
\setupbodyfont[xits]
\definefont[tt][APL sa 1]

\setuppapersize[letter][letter]
\setupwhitespace[medium]

\starttext
\startfrontmatter
\title{HPAPL: The Compiler}

\completecontent
\vfill
Copyright $\copyright$ 2012 Aaron W. Hsu $⟨${\tt arcfide@sacrideo.us}$⟩$

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
\stopfrontmatter

\startbodymatter
\chapter{Introduction}

This is a first simple attempt to get a small piece of each element of 
the HPAPL compiler working and running.

\defchunk{HPAPL Namespace}
:Namespace HPAPL
  ⎕IO ⎕ML←0 0
  /BTEX\chunk{Parsing Utilities}/ETEX
  /BTEX\chunk{File Utilities}/ETEX
  /BTEX\chunk{AST Utilities}/ETEX
  /BTEX\chunk{HPAPL Code}/ETEX
  ∇ Reload;N
    'HPAPL Namespace' #.ConTeXtLP.Tangle './hpapl.tex' './hpapl.dyalog'
    N←#.⎕SE.SALT.Load './hpapl -Target=#'
    ⎕←'Loaded: ',⍕N
  ∇
  Compile←{
    toks←Tokenize ⍺
    ast rest←0 ParseStmts toks
    ⍵ OutputC ast
  }
:EndNamespace
\stopchunk

\chapter{Some Basic Utilities}

Throughout the code we use a few utilities that make our life easier. 
We will describe them all here up front before we delve into the meat.
Firstly, we have a set of functions that help us with parsing.  
You might call these parser combinators.  We produce a syntax tree that 
is a linearization of a tree into a matrix of shape $N 3$ where $N$ is 
the number of nodes and 3 is the number of columns.  Column 1 is the 
depth of that node, column 2 the name or type of the node, and 
column 3 is any data that is stored in that field, in the form of a 
vector of fields.

\defchunk{Parsing Utilities}
ParseTok←{⍵⍵=⊃⊃⍵: (⊂1 3⍴⍺,(⊂⍺⍺),⊂1⊃⊃⍵),⊂1↓⍵ ⋄ ⎕SIGNAL 2}
ParseSeq←{2:: ⎕SIGNAL 2 ⋄ n1 r1←⍺ ⍺⍺ ⍵ ⋄ n2 r2←⍺ ⍵⍵ r1 ⋄ (⊂n1⍪n2),⊂r2}
ParseChoice←{2:: ⍺ ⍵⍵ ⍵ ⋄ ⍺ ⍺⍺ ⍵}
ParseStar←{2:: ⎕SIGNAL 2
  0=⍴⍵: (⊂0 3⍴⍬),⊂⍵
  2:: (⊂0 3⍴⍬),⊂⍵
  ast rst←⍺ ⍺⍺ ⍵
  nxt nst←⍺ (⍺⍺ ParseStar) rst
  (⊂ast⍪nxt),⊂nst
}
\stopchunk

Next, we want to be able to work with files easily, so we have a few 
utilities for printing, writing, and so forth.

\defchunk{File Utilities}
OpenFile←{
  22:: ⍵ ⎕NCREATE 0
  tie←⍵ ⎕NTIE 0
  _←⍵ ⎕NERASE tie
  ⍵ ⎕NCREATE 0
}
∇ TIE println DATA
  TIE print DATA
  (⎕UCS 10)⎕NAPPEND TIE,80
∇
print←{(⎕UCS 'UTF-8' ⎕UCS ⍵)⎕NAPPEND ⍺,80}
\stopchunk

And then we have some utilities that we use when we want to work with 
the AST.

\defchunk{AST Utilities}
Children←{
  c←⊃⍵ ⋄ d←0⌷[1]⍵
  ((0,~∨\c=1↓d)∧(1+c)=d)⊂[0]⍵}
\stopchunk

\chapter{Parsing HPAPL Syntax}

This chapter focuses on getting the HPAPL program from strings into 
a reasonable representation, and getting back into a string again. 

Let's start with a tokenizer.

\startformula
\hbox{\bf Insert missing tokens list here.}
\stopformula

\defchunk{HPAPL Code}
TokPats←'[0-9]+' '[a-zA-Z][a-zA-Z0-9]*' '←' '⎕'
TokTypes←'Number' 'Variable' 'Assignment' 'Quad'
Tokenize←{
  MakeTok←{⍵.PatternNum,⊂⍵.Block[(⊃⍵.Offsets)+⍳⊃⍵.Lengths]}
  (⎕NUNTIE tie)⊢(TokPats⎕S MakeTok) tie←⍵ ⎕NTIE 0
}
\stopchunk

Now let's move to parsing. Here 
is the grammar of our language:\index{Language Grammar}

\define[1]\term{\hbox{\tt #1}}

\startformula
\eqalign{
  Statements  &\Rightarrow Assignment*
  Assignment  &\Rightarrow PrintAssign\ |\ VarAssign
  PrintAssign &\Rightarrow \term{⎕}\term{←}\ Expression\cr
  VarAssign   &\Rightarrow Variable\ \term{←}\ Expression\cr
  Expression  &\Rightarrow Variable\ |\ Number\cr
  Variable    &\Rightarrow \term{variable}\cr
  Number      &\Rightarrow \term{number}\cr
}
\stopformula

We can stranslate the above syntax tree into a set of parsers using 
parser combinators pretty easily.  The goal here is to ensure an LL(1) 
grammar so this task will always be easy.  Here is the encoding 
using out parsing combinators.

\defchunk{HPAPL Code}
Choose←{2:: ⍵⍵ ⍵ ⋄ ⍺⍺ ⍵}
Both←{2:: ⎕SIGNAL 2 ⋄ ⍵⍵ ⍺⍺ ⍵}
Inc←{1+⍵}
Bad←{⎕SIGNAL 2}
IncInc←{Bad Both Inc Both Inc ⍵}
Test←{IncInc Choose Inc ⍵}

ParseNum←('Number' ParseTok 0)
ParseVar←('Variable' ParseTok 1)
ParseAssgnTok←('AssignTok' ParseTok 2)
ParseQuadTok←('Quad' ParseTok 3)
ParseExp←(ParseVar ParseChoice ParseNum)
ParseVarAssgn←{2:: ⎕SIGNAL 2
  n r←(1+⍺)(ParseVar ParseSeq ParseAssgnTok ParseSeq ParseExp) ⍵
  (⊂(⍺,(⊂'VarAssign'),⊂⍬)⍪n[0 2;]),⊂r
}
ParsePrintAssgn←{2:: ⎕SIGNAL 2
  n r←(1+⍺)(ParseQuadTok ParseSeq ParseAssgnTok ParseSeq ParseExp) ⍵
  (⊂(⍺,(⊂'PrintAssign'),⊂⍬)⍪1 3⍴2⌷n),⊂r
}
ParseAssgn←(ParseVarAssgn ParseChoice ParsePrintAssgn)
ParseStmts←{
  ast rst←(1+⍺)(ParseAssgn ParseStar) ⍵
  (⊂(⍺,(⊂'Statements'),⊂⍬)⍪ast),⊂rst
}
\stopchunk

\chapter{Outputting C Code}

Let's take our basic syntax tree and print out a C version of the 
program.

\defchunk{HPAPL Code}
∇ fn OutputC ast;tie
  tie←OpenFile fn
  tie println '#include <stdio.h>'
  tie println '#include <stdlib.h>'
  tie println ''
  tie println 'int main(int argc, char *argv[])'
  tie println '{'
  tie print   '  '
  tie OutputCStmts ast
  tie println '  return 0;'
  tie println '}'
  ⎕NUNTIE tie
∇

OutputCStmts←{⍺∘OutputCAssgn¨Children ⍵}
OutputCAssgn←{
  type←⊃1⌷0⌷⍵
  type≡'VarAssign': ⍺ OutputCVarAssgn ⍵
  type≡'PrintAssign': ⍺ OutputCPrintAssgn ⍵
}
∇ tie OutputCVarAssgn ast
  var exp←Children ast
  tie OutputCVar var
  tie print ' = '
  tie OutputCExp exp
  tie println ';'
∇
∇ tie OutputCPrintAssgn ast
  exp←⊃Children ast
  tie print 'printf("%d\n", '
  tie OutputCExp exp
  tie println ');'
∇

OutputCExp←{⍺ print ⊃0 2⌷⍵}
OutputCVar←{⍺ print ⊃0 2⌷⍵}
\stopchunk

\stopbodymatter

\startappendices

\chapter[designpatterns]{Design Patterns}

\section[visitorpattern]{Visitor Pattern}

\section[factorypattern]{Factory Pattern}

\stopappendices

\startbackmatter
\completeindex
\stopbackmatter

\stoptext
