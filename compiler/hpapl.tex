\input ConTeXtLPMacros

\definefontsynonym[APL][Apl385]
\setupbodyfont[xits]
\definefont[tt][APL sa 1]

\setuppapersize[letter][letter]
\setupwhitespace[medium]

\starttext
\startfrontmatter
\title{HPAPL: The Compiler}

\completecontent
\vfill
Copyright $\copyright$ 2012 Aaron W. Hsu $⟨${\tt arcfide@sacrideo.us}$⟩$

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
\stopfrontmatter

\startbodymatter
\chapter{Introduction}

This is a first simple attempt to get a small piece of each element of 
the HPAPL compiler working and running.

\defchunk{HPAPL Namespace}
:Namespace HPAPL
  ⎕IO ⎕ML←0 0
  /BTEX\chunk{HPAPL Code}/ETEX
  ∇ Reload;N
    'HPAPL Namespace' #.ConTeXtLP.Tangle './hpapl.tex' './hpapl.dyalog'
    N←#.⎕SE.SALT.Load './hpapl -Target=#'
    ⎕←'Loaded: ',⍕N
  ∇
:EndNamespace
\stopchunk


\chapter{Parsing and Pretty Printing}

This chapter focuses on getting the HPAPL program from strings into 
a reasonable representation, and getting back into a string again. Here 
is the grammar of our language:\index{Language Grammar}

\define[1]\term{\hbox{\tt #1}}

\startformula
\eqalign{
  Statements &\Rightarrow Assignment*
  Assignment &\Rightarrow Variable\ \term{←}\ Expression\cr
  Expression &\Rightarrow Variable\ |\ Number\cr
  Variable   &\Rightarrow \hbox{\tt variable}\cr
  Number     &\Rightarrow \hbox{\tt number}\cr
}
\stopformula

Let's start with a tokenizer.

\defchunk{HPAPL Code}
TokPats←'[0-9]+' '[a-zA-Z][a-zA-Z0-9]*' '←'
TokTypes←'Number' 'Variable' 'Assignment'
Tokenize←{
  MakeTok←{⍵.PatternNum,⊂⍵.Block[(⊃⍵.Offsets)+⍳⊃⍵.Lengths]}
  (⎕NUNTIE tie)⊢(TokPats⎕S MakeTok) tie←⍵ ⎕NTIE 0
}
\stopchunk

Now let's move to parsing.  We use a parser combinator style of parser. 
In the first place, we need to have a simple way to parse a specific 
token and create an entry in our syntax tree when we 
parse.  Our syntax tree is a three-column matrix where the first 
column is the nesting level, the second column is the node name or type, 
and the third is a vector of values or fields for that node.

\defchunk{HPAPL Code}
ParseTok←{⍵⍵=⊃⊃⍵: (⊂1 3⍴⍺,(⊂⍺⍺),⊂1⊃⊃⍵),⊂1↓⍵ ⋄ ⎕SIGNAL 2}
ParseNum←('Number' ParseTok 0)
ParseVar←('Variable' ParseTok 1)
ParseAssgnTok←('AssignTok' ParseTok 2)
\stopchunk

Now that we have basic parsers for the tokens to normal AST nodes, 
it's important to be able to parse a sequence of these.

\defchunk{HPAPL Code}
ParseSeq←{n1 r1←⍺ ⍺⍺ ⍵ ⋄ n2 r2←⍺ ⍵⍵ r1 ⋄ (⊂n1⍪n2),⊂r2}
ParseAssgn←{
  n r←(1+⍺)(ParseVar ParseSeq ParseAssgnTok ParseSeq ParseExp) ⍵
  (⊂(⍺,(⊂'Assignment'),⊂⍬)⍪n[0 2;]),⊂r
}
\stopchunk

The \cid{ParseVarOrNum} parser deals with a choice, which we write down 
here.

\defchunk{HPAPL Code}
ParseChoice←{2:: ⍺ ⍵⍵ ⍵ ⋄ ⍺ ⍺⍺ ⍵}
ParseExp←(ParseVar ParseChoice ParseNum)
\stopchunk

To extend this out to handle a series of assignment statements, let's 
add the ability to parse $\epsilon$, which we can then use to parse 
statements.  I am having a bit of trouble here, because if I parse 
the statements in the other order, without the $\epsilon$ being 
first, I get an error.

\defchunk{HPAPL Code}
ParseStar←{
  0=⍴⍵: (⊂0 3⍴⍬),⊂⍵
  2:: (⊂0 3⍴⍬),⊂⍵⊣⎕←'Caught'
  ast rst←⍺ ⍺⍺ ⍵
  nxt nst←⍺ (⍺⍺ ParseStar) rst
  (⊂ast⍪nxt),⊂nst
}
ParseStmts←{
  ast rst←(1+⍺)(ParseAssgn ParseStar) ⍵
  (⊂(⍺,(⊂'Statements'),⊂⍬)⍪ast),⊂rst
}
\stopchunk

\chapter{Outputting C Code}

Let's take our basic syntax tree and print out a C version of the 
program.

\defchunk{HPAPL Code}
∇ fn OutputC ast;tie
  tie←OpenFile fn
  tie println '#include <stdio.h>'
  tie println '#include <stdlib.h>'
  tie println ''
  tie println 'int main(int argc, char *argv[])'
  tie println '{'
  tie print   '  '
  tie OutputCStmts ast
  tie println '  return 0;'
  tie println '}'
  ⎕NUNTIE tie
∇

OutputCStmts←{⍺∘OutputCAssgn¨Children ⍵}

∇ tie OutputCAssgn ast
  var exp←Children ast
  tie OutputCVar var
  tie print ' = '
  tie OutputCExp exp
  tie println ';'
∇

OutputCExp←{⍺ print ⊃0 2⌷⍵}
OutputCVar←{⍺ print ⊃0 2⌷⍵}
\stopchunk

We use the following functions to manipulate the ast and get what we 
want out of it.

\defchunk{HPAPL Code}
Children←{
  c←⊃⍵ ⋄ d←0⌷[1]⍵
  ((0,~∨\c=1↓d)∧(1+c)=d)⊂[0]⍵}
ChildFields←{
  cur←⊃0⌷⍺
  bv←0,~∨\cur=1↓0⌷[1]⍺
  kids←bv⌿⍺
  ((1⌷[1]kids)⍳⊂⍵)⌷kids
}

\stopchunk


We use the following utilities for the above.

\defchunk{HPAPL Code}
OpenFile←{
  22:: ⍵ ⎕NCREATE 0
  tie←⍵ ⎕NTIE 0
  _←⍵ ⎕NERASE tie
  ⍵ ⎕NCREATE 0
}
∇ TIE println DATA
  TIE print DATA
  (⎕UCS 10)⎕NAPPEND TIE,80
∇
print←{(⎕UCS 'UTF-8' ⎕UCS ⍵)⎕NAPPEND ⍺,80}
\stopchunk

\chapter{Compiler}

Let's use the following procedure to do the basic compilation.

\defchunk{HPAPL Code}
Compile←{
  toks←Tokenize ⍺
  ast rest←0 ParseStmts toks
  ⍵ OutputC ast
}
\stopchunk


\stopbodymatter

\startappendices

\chapter[designpatterns]{Design Patterns}

\section[visitorpattern]{Visitor Pattern}

\section[factorypattern]{Factory Pattern}

\stopappendices

\startbackmatter
\completeindex
\stopbackmatter

\stoptext
