DISPLAY←{
        ⎕IO ⎕ML←0
        ⍺←1 ⋄ chars←⍺⊃'..''''|-' '┌┐└┘│─'
        tl tr bl br vt hz←chars
        box←{
                vrt hrz←(¯1+⍴⍵)⍴¨vt hz
                top←(hz,'⊖→')[¯1↑⍺],hrz
                bot←(⊃⍺),hrz
                rgt←tr,vt,vrt,br
                lax←(vt,'⌽↓')[¯1↓1↓⍺],¨⊂vrt
                lft←⍉tl,(↑lax),bl
                lft,(top⍪⍵⍪bot),rgt
        }
        deco←{⍺←type open ⍵ ⋄ ⍺,axes ⍵}
        axes←{(-2⌈⍴⍴⍵)↑1+×⍴⍵}
        open←{(1⌈⍴⍵)⍴⍵}
        trim←{(~1 1⍷∧⌿⍵=' ')/⍵}
        type←{{(1=⍴⍵)⊃'+'⍵}∪,char¨⍵}
        char←{⍬≡⍴⍵:hz ⋄ (⊃⍵∊'¯',⎕D)⊃'#~'}∘⍕
        line←{(6≠10|⎕DR' '⍵)⊃' -'}
        {
                0=≡⍵:' '⍪(open ⎕FMT ⍵)⍪line ⍵
                1 ⍬≡(≡⍵)(⍴⍵):'∇' 0 0 box ⎕FMT ⍵
                1=≡⍵:(deco ⍵)box open ⎕FMT open ⍵
                ('∊'deco ⍵)box trim ⎕FMT ∇¨open ⍵
        }⍵
}
