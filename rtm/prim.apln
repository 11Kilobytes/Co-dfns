:Namespace prim

⍝ Basic system functions
q_signal←'q_signal_ibeam'⌶
q_dr←'q_dr_ibeam'⌶

⍝ System constants
spn←'span_array'⌶⌶⌶⌶

⍝ Utilities
squeeze←'squeeze_ibeam'⌶
is_simple←'is_simple_ibeam'⌶
is_numeric←'is_numeric_ibeam'⌶
is_char←'is_char_ibeam'⌶
is_integer←'is_integer_ibeam'⌶
is_bool←{11≡⎕DR squeeze ⍵}
is_span←'is_span_ibeam'⌶
max_shp←{⍺←0 ⋄ ⍺('max_shp_ibeam'⌶)⍵}
has_nat_vals←'has_nat_vals_ibeam'⌶
chk_scl←{⍺←0 ⋄ (⍴⍺)≡⍴⍵:0 ⋄ 1≡≢,⍺:0 ⋄ 1≡≢,⍵:0 ⋄ (≢⍴⍺)≡≢⍴⍵:⎕SIGNAL 5 ⋄ ⎕SIGNAL 4}
chk_valid_shape←{
	{rnk←≢⍴⍵ ⋄ 0≡rnk:0 ⋄ 1≡rnk:0 ⋄ 1}⍵:⎕SIGNAL 4
	{has_nat_vals ⍵:0 ⋄ 1}⍵:⎕SIGNAL 11
	0
}
both_simple←{is_simple ⍺:is_simple ⍵ ⋄ 0}
both_numeric←{is_numeric ⍺:is_numeric ⍵ ⋄ 0}
both_integer←{is_integer ⍺:is_integer ⍵ ⋄ 0}
both_char←{is_char ⍺:is_char ⍵ ⋄ 0}
both_bool←{is_bool ⍺:is_bool ⍵ ⋄ 0}
any←'any_ibeam'⌶
numeric←{⍺←⊢ ⋄ (⍺⊣0)both_numeric ⍵:⍺ ⍺⍺ ⍵ ⋄ ⎕SIGNAL 11}
ambiv←{⍺←⊢ ⋄ 1 same ⍺ 1:⍺⍺ ⍵ ⋄ ⍺ ⍵⍵ ⍵}
veach←'veach_ibeam'⌶⌶
scalar←{
	⍺ chk_scl ⍵: 
	s←⍺ max_shp ⍵
	0≡≢,⍺:s⍴0
	0≡≢,⍵:s⍴0
	⍺ both_simple ⍵:s⍴⍺ ⍺⍺ ⍵
	s⍴squeeze ⍺ ∇ veach ⍵
}

⍝ Assignment
idx_shp_check←{
	326≢⎕DR ⍺:⎕SIGNAL 99
	1≢≢,⍺:⎕SIGNAL 16
	idx←⊃⍺ ⋄ ic←≢,idx ⋄ vc←≢,⍵
	(1≢vc)∧ic≢vc:⎕SIGNAL 4
	idx
}
idx_rng_check←{
	0≡≢,⍺:⍺
	0≡is_integer⊢idx←squeeze ⍺:⎕SIGNAL 11
	any(idx<0)∨idx>≢,⍵:⎕SIGNAL 3
	idx
}
set←'set_ibeam'⌶

⍝ Bracket Indexing
brk←{
	(≢⍴⍺)≢≢,⍵:⎕SIGNAL 4
	⍵⌷⍺
}

⍝ ⊢: Right
rgt←{⍵}

⍝ ⊣: Left
lft←{⍵} ambiv {⍺}

⍝ ⍴: Shape / Reshape
reshape←{chk_valid_shape ⍺: ⋄ ⍺('reshape_ibeam'⌶)⍵}
rho←('shape_ibeam'⌶) ambiv reshape

⍝ ,: Ravel / Catenate
cat←{
	israv←noax←0 ⋄ ⍺←israv←1 ⋄ lr←≢⍴⍺ ⋄ rr←≢⍴⍵ ⋄ axis⍠←noax←1
	israv:{
		noax:'ravel_ibeam'⌶ ⍵
		1≢≢,axis:⎕SIGNAL 16
		axis=⌈axis:⍵
		((pr↑s),1,(pr←⌈axis)↓s←⍴⍵)⍴⍵
	}⍵
	rk←lr⌈rr ⋄ axis⍠←¯1+rk
	(0≡lr)∧(0≡rr):(,⍺),,⍵
	ar←≢⍴axis:⎕SIGNAL 4 ⋄ 1≢≢,axis:⎕SIGNAL 5 
	~is_numeric axis:⎕SIGNAL 11 ⋄ axis≤¯1:⎕SIGNAL 11
	axis≥rk:⎕SIGNAL 4 ⋄ (lr≢0)∧(rr≢0)∧1<|lr-rr:⎕SIGNAL 4
	axis≠fx←⌈axis:⍺{
		(lr≢0)∧(rr≢0)∧lr≠rr:⎕SIGNAL 4
		(,[axis]⍣(lr≢0)⊢⍺),[fx],[axis]⍣(rr≢0)⊢⍵
	}⍵
	(pr↑⍴⍺)∨.≠(pr←(lr⌊rr)-lr=rr)↑⍴⍵:⎕SIGNAL 5
	lx←{fx≥⍺:1 ⋄ fx⌷⍴⍵} ⋄ vc←{⍺≡0:c ⋄ ≢⍵}
	fx←(lr⌈rr)-1 ⋄ a←,⍺ ⋄ ax←lr lx ⍺ ⋄ w←,⍵ ⋄ wx←rr lx ⍵
	s←⍴⍺{lr>rr:⍺ ⋄ ⍵}⍵ ⋄ s[fx]←ax+wx ⋄ c←×⌿fx↑s ⋄ ac←lr vc a ⋄ wc←rr vc w 
	z←(c×fx⌷s)⍴0 ⋄ z[i+wx×⌊(i←⍳ac)÷ax]←a ⋄ z[i+ax×1+⌊(i←⍳wc)÷wx]←w
	s⍴z	
}

⍝ Table / Catenate First
ctf←{⎕SIGNAL 16}

⍝ ≡: Depth / Same
depth←{
   is_simple ⍵:{0≡≢⍴⍵:0 ⋄ 1}⍵
   0≡≢,⍵:(×d)+d←∇⊃⍵
   nd←1+mx←⌈⌿pd←|d←∇¨,⍵
   ∨⌿(d<0)∨pd≠mx:-nd
   nd
}
same←'same_ibeam'⌶
eqv←depth ambiv same

⍝ ≢: Tally / Not Same
nqv←{⍬≡⍴⍵:1 ⋄ ⊃⍴⍵} ambiv ('nqv_ibeam'⌶)

⍝ ⌷: Materialize | Squad indexing
sqd_vec←{
	is_span ⍺:⍵
	0≡is_integer squeeze ⍺:⎕SIGNAL 11
	0≡≢,⍺:(⍴⍺)⍴0⍴⍵
	any(⍺<0)∨⍺>≢,⍵:⎕SIGNAL 3
	(⍴⍺)⍴⍺('index_ibeam'⌶)⍵
}
sqd←{⍺←⊢
	1≡⍺ 1:⍵
	{0≡⍵:0 ⋄ 1≡⍵:0 ⋄ 1}≢⍴i←⍺⊣0:⎕SIGNAL 4
	(1≡c←≢i){⍺:⍵ ⋄ 0}1≡≢⍴⍵:(⊃i)sqd_vec ⍵
	c>≢⍴⍵:⎕SIGNAL 5
	i←i{is_span ⍺:⍳ws[⍵] ⋄ ⍺}¨c↑⍳≢ws←⍴⍵
	j←⊃∘.+⌿(i×c↑1↓o),⊂(c↓⍴⍵)⍴⍳c⌷o←⌽×⍀1,⌽⍴⍵
	j sqd_vec ⍵
}

⍝ ⍳: Index Generate | Index Of
index_gen←{
	chk_valid_shape ⍵: 
	0≡≢,⍵:⊂⍬
	(,0)≡,⍵:⍬
	1≡≢,⍵:'index_gen_vec'⌶ ⍵
	⎕SIGNAL 16
}
index_of←{⎕SIGNAL 16}
iot←index_gen ambiv index_of

⍝ ⊃: First / Pick
dis←('disclose_ibeam'⌶) ambiv {⎕SIGNAL 16}

⍝ ⊂: Enclose / Partition
enclose←{
	0≡≡⍵:⍵
	'enclose_ibeam'⌶ ⍵
}
par←enclose ambiv {⎕SIGNAL 16}

⍝ +: Conjugate | Addition
conjugate←{1289≡⎕DR squeeze ⍵:'conjugate_vec_ibeam'⌶ ⍵ ⋄ ⍵}
add←conjugate ambiv ('add_vec_ibeam'⌶ numeric scalar)

⍝ -: Negate | Subtract
sub←{0-⍵} ambiv ('sub_vec_ibeam'⌶ numeric scalar)

⍝ ×: Sign | Multiply
sign←{
	1289=⎕DR ⍵:⍵÷|⍵
	(-⍵<0)+0<⍵
}
mul←sign ambiv ('mul_vec_ibeam'⌶ numeric scalar)

⍝ ÷: Reciprocal | Divide
div←{1÷⍵} ambiv ('div_vec_ibeam'⌶ numeric scalar)

⍝ |: Absolute Value | Residue
absolute←{
	0≡is_numeric ⍵:⎕SIGNAL 11
	1289≡⎕DR ⍵:⎕SIGNAL 11
	'abs_vec_ibeam'⌶ ⍵
}
residue←{⍵-⍺×⌊⍵÷⍺+0=⍺}
res←absolute numeric ambiv residue

⍝ ⌊: Floor | Minimum
floor_array←{
	is_integer ⍵:⍵
	1289=⎕DR ⍵:⎕SIGNAL 16
	'floor_vec_ibeam'⌶ ⍵
}
min←floor_array numeric ambiv ('min_vec_ibeam'⌶ numeric scalar)

⍝ ⌈: Ceiling | Maximum
ceil_array←{
	is_integer ⍵:⍵
	1289=⎕DR ⍵:-⌊-⍵
	'ceil_vec_ibeam'⌶ ⍵
}
max←ceil_array numeric ambiv ('max_vec_ibeam'⌶ numeric scalar)

⍝ *: Exponent | Power
exp←'exp_vec_ibeam'⌶ numeric ambiv ('pow_vec_ibeam'⌶ numeric scalar)

⍝ ⍟: Natural Logarithm | Logarithm
log←'nlg_vec_ibeam'⌶ numeric ambiv ('log_vec_ibeam'⌶ numeric scalar)

⍝ ○: Pi Times | Trigonometric Functions
pitimes←{3.14159265358979323846×⍵}
trig←{
	0≠≡⍺:⍺ ∇¨⍵
	0=⍺:(1-⍵*2)*.5
	1=⍺:'sin_vec_ibeam'⌶ ⍵
	¯1=⍺:'arcsin_vec_ibeam'⌶ ⍵
	2=⍺:'cos_vec_ibeam'⌶ ⍵
	¯2=⍺:'arccos_vec_ibeam'⌶ ⍵
	3=⍺:'tan_vec_ibeam'⌶ ⍵
	¯3=⍺:'arctan_vec_ibeam'⌶ ⍵
	4=⍺:(1+⍵*2)*.5
	¯4=⍺:(⍵+1)×((⍵-1)÷⍵+1)*0.5
	5=⍺:'sinh_vec_ibeam'⌶ ⍵
	¯5=⍺:'arcsinh_vec_ibeam'⌶ ⍵
	6=⍺:'cosh_vec_ibeam'⌶ ⍵
	¯6=⍺:'arccosh_vec_ibeam'⌶ ⍵
	7=⍺:'tanh_vec_ibeam'⌶ ⍵
	¯7=⍺:'arctanh_vec_ibeam'⌶ ⍵
	8=⍺:(-1+⍵*2)*0.5
	¯8=⍺:-8○⍵
	9=⍺:{1289≡⎕DR ⍵:'realpart_vec_ibeam'⌶ ⍵ ⋄ ⍵}⍵
	¯9=⍺:⍵
	10=⍺:|⍵
	¯10=⍺:+⍵
	11=⍺:{1289≡⎕DR ⍵:'imagpart_vec_ibeam'⌶ ⍵ ⋄ (⍴⍵)⍴0}⍵
	¯11=⍺:⍵×0J1
	12=⍺:'PHASE(⍵) NOT IMPLEMENTED'⎕SIGNAL 16
	¯12=⍺:*⍵×0J1
	⎕SIGNAL 11
}
cir←pitimes numeric ambiv (trig numeric scalar)

⍝ !: Factorial | Binomial
binomial←{
	∨⌿,(⍵=⌊⍵)∧(⍵<0)∧⍺≠⌊⍺:⎕SIGNAL 11
	(!⍵)÷(!⍺)×!⍵-⍺
}
fac←'factorial_vec_ibeam'⌶ numeric ambiv (binomial numeric scalar)

⍝ ~: Not | Without
notscl←{is_bool ⍵:'not_vec_ibeam'⌶ ⍵ ⋄ ⎕SIGNAL 11}
without←{⎕SIGNAL 16}
not←notscl ambiv without

⍝ ∧: And
logand←{
	⍺ both_bool ⍵:⍺('and_vec_ibeam'⌶)⍵
	⍺×⍵÷z+0=z←⍺∨⍵
}
and←{⎕SIGNAL 2}ambiv (logand numeric scalar)

⍝ ∨: Or
logor←{
	⍺ both_bool ⍵:⍺('lor_vec_ibeam'⌶)⍵
	(is_bool ⍺)∧is_integer ⍵:⍺+(~⍺)×|⍵
	(is_bool ⍵)∧is_integer ⍺:⍵+(~⍵)×|⍺
	⍺ both_integer ⍵:(|⍺){~∨⌿m←⍵≠0:⍺ ⋄ ((⍺×~m)+m×⍵)∇(⍵×~m)+m×⍵|⍺}|⍵
	(|⍺){~∨⌿m←⍵>1e¯12:⍺ ⋄ ((⍺×~m)+m×⍵)∇(⍵×~m)+m×⍵|⍺}|⍵
}
lor←{⎕SIGNAL 2}ambiv (logor numeric scalar)

⍝ ⍲: Not and
nan←{⎕SIGNAL 2}ambiv{⍺ both_bool ⍵:~⍺∧⍵ ⋄ ⎕SIGNAL 11}

⍝ ⍱: Not Or
nor←{⎕SIGNAL 2}ambiv{⍺ both_bool ⍵:~⍺∨⍵ ⋄ ⎕SIGNAL 11}

⍝ <: Less than
lessthan←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('lth_vec_ibeam'⌶)⍵
}
lth←{⎕SIGNAL 2} ambiv (lessthan numeric scalar)

⍝ ≤: Less than or equal
lesseql←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('lte_vec_ibeam'⌶)⍵
}
lte←{⎕SIGNAL 2}ambiv (lesseql numeric scalar)

⍝ =: Equal
eql_vec←'eql_vec_ibeam'⌶
equal←{
	⍺ both_numeric ⍵:⍺ eql_vec ⍵
	⍺ both_char ⍵:⍺ eql_vec ⍵
	0
}
eql←{⎕SIGNAL 2}ambiv (equal scalar)

⍝ ≥: Greater than or equal
greatereql←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('gte_vec_ibeam'⌶)⍵
}
gte←{⎕SIGNAL 2}ambiv (greatereql numeric scalar)

⍝ >: Greater than
greaterthan←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('gth_vec_ibeam'⌶)⍵
}
gth←{⎕SIGNAL 2}ambiv (greaterthan numeric scalar)

⍝ ≠: Not Equal | First Occurrence
firstocc←{⎕SIGNAL 16}
neq_vec←'neq_vec_ibeam'⌶
noteq←{
	⍺ both_numeric ⍵:⍺ neq_vec ⍵
	⍺ both_char ⍵:⍺ neq_vec ⍵
	1
}
neq←firstocc ambiv (noteq scalar)

⍝ ↑: Mix | Take
mix←{⎕SIGNAL 16}
take←{(⊂⍳⍺)⌷⍵}
tke←mix ambiv take

⍝ ↓: Split | Drop
split←{⎕SIGNAL 16}
drop←{(⊂⍺+⍳(≢⍵)-⍺)⌷⍵}
drp←split ambiv drop

⍝ ⊖: Rerverse First | Rotate First
rtf←{⎕SIGNAL 16}

⍝ ⌽: Reverse Last | Rotate Last
reverse_last←{(⊂(≢⍵)-1+⍳≢⍵)⌷⍵}
rotate_last←{⎕SIGNAL 16}
rot←reverse_last ambiv rotate_last

⍝ ⍉: Transpose
trn←{⎕SIGNAL 16}

⍝ ∪: Unique | Union
unq←{⎕SIGNAL 16}

⍝ ?: Roll | Deal
rol←{⎕SIGNAL 16}

⍝ ⍨: Commute
com←{2=⎕NC'⍺⍺':⍺⍺ ⋄ ⍺←⍵ ⋄ ⍵ ⍺⍺ ⍺}

⍝ ¨: Each
map←{⍺←⊢ ⋄ ⍺ chk_scl ⍵: ⋄ s←⍺ max_shp ⍵ ⋄ s⍴squeeze ⍺ ⍺⍺ veach ⍵}

⍝ ⌿: Reduce First | N-wise Reduce First
reduce←{
	0≡≢⍴⍵:∇,⍵ ⋄ s←⍴⍵ ⋄ c←⊃s ⋄ zs←1↓s
	0≡c:zs⍴⍺⍺('identity_ibeam'⌶⌶)⍬ ⋄ 1≡c:zs⍴⍵ ⋄ op←⍺⍺¨
	z⊣(⌽⍳c-1){⍬⊣z⊢←(⍺⌷⍵)op z}¨⊂⍵⊣z←(c-1)⌷⍵
}
nwreduce←{op←⍺⍺¨
	0≡is_integer ⍺:⎕SIGNAL 11 ⋄ rc←1+≢⍵ ⋄ rc<⍺:⎕SIGNAL 5 ⋄ rc-←|⍺
	z⊣(⌽⍳⍺-1){z⊢←((⊂⍺+⍳rc)⌷⍵)op z}¨⊂⍵⊣z←(⍺-1)↓⍵
}
rdf←{⍺←⊢ ⋄ 1<≢⍴⍺⊣0:⎕SIGNAL 4 ⋄ 1≢≢,⍺⊣0:⎕SIGNAL 5
	1≡⍺ 1:⍺⍺ reduce ⍵ ⋄ ⍺(⍺⍺ nwreduce)⍵
}

⍝ ⌿⌿: Replicate First
rpf←{⎕SIGNAL 16}

⍝ //: Replicate
rep←{⎕SIGNAL 16}

⍝ ⍀: Scan First
scf←{X←,⍵ ⋄ fn←⍺⍺ ⋄ squeeze (⍴⍵)⍴{fn⌿X[⍳⍵]}¨1+⍳≢⍵}

⍝ .: Inner Product
dot←{⍺⍺⌿⍺ ⍵⍵¨ ⍵}

⍝ ∘.: Outer Product
oup←{i←⍳(≢,⍺)×y←≢,⍵ ⋄ ((⍴⍺),⍴⍵)⍴(,⍺)[⌊i÷y]⍺⍺¨(,⍵)[y|i]}

⍝ ⍣: Power Operator
pow←{0≡⍵⍵:⍵ ⋄ ⍺←⊢ ⋄ ⍺ ⍺⍺∇∇(⍵⍵-1)⊢⍺ ⍺⍺ ⍵}

⍝ ∘: Compose Operator
jot←{
	type←⎕NC'⍺' '⍺⍺' '⍵⍵'
	type≡2 3 3:⍺ ⍺⍺ ⍵⍵ ⍵
	type≡0 3 3:⍺⍺ ⍵⍵ ⍵
	type≡0 2 3:⍺⍺ ⍵⍵ ⍵
	type≡0 3 2:⍵ ⍺⍺ ⍵⍵
	⎕SIGNAL 2
}

:EndNamespace
