:Namespace cdf_prim

⍝ Basic system functions
q_signal←'q_signal_ibeam'⌶
q_dr←'q_dr_ibeam'⌶
q_veach←'q_veach_ibeam'⌶⌶
q_ambiv←'q_ambiv_ibeam'⌶⌶⌶

⍝ Utilities
squeeze←'squeeze_ibeam'⌶
is_simple←'is_simple_ibeam'⌶
is_numeric←'is_numeric_ibeam'⌶
is_char←'is_char_ibeam'⌶
is_integer←'is_integer_ibeam'⌶
is_bool←{11≡⎕DR squeeze ⍵}
max_shp←'max_shp_ibeam'⌶
has_nat_vals←'has_nat_vals_ibeam'⌶
chk_scl←{(⍴⍺)≡⍴⍵:0 ⋄ 1≡≢,⍺:0 ⋄ 1≡≢,⍵:0 ⋄ (≢⍴⍺)≡≢⍴⍵:⎕SIGNAL 5 ⋄ ⎕SIGNAL 4}
chk_valid_shape←{
	{rnk←≢⍴⍵ ⋄ 0≡rnk:0 ⋄ 1≡rnk:0 ⋄ 1}⍵:⎕SIGNAL 4
	{has_nat_vals ⍵:0 ⋄ 1}⍵:⎕SIGNAL 11
	0
}
both_simple←{is_simple ⍺:is_simple ⍵ ⋄ 0}
both_numeric←{is_numeric ⍺:is_numeric ⍵ ⋄ 0}
both_integer←{is_integer ⍺:is_integer ⍵ ⋄ 0}
both_char←{is_char ⍺:is_char ⍵ ⋄ 0}
both_bool←{is_bool ⍺:is_bool ⍵ ⋄ 0}
numeric←{⍺←⊢ ⋄ (⍺⊣0)both_numeric ⍵:⍺ ⍺⍺ ⍵ ⋄ ⎕SIGNAL 11}
scalar←{⍺ chk_scl ⍵: ⋄ s←⍺ max_shp ⍵ ⋄ ⍺ both_simple ⍵:s⍴⍺ ⍺⍺ ⍵ ⋄ s⍴⍺ ∇ ⎕VEACH ⍵}

⍝ Assignment
set←'set_ibeam'⌶

⍝ ⊢: Right
rgt←{⍵}

⍝ ⊣: Left
lft←{⍵} ⎕AMBIV {⍺}

⍝ ⍴: Shape / Reshape
reshape←{chk_valid_shape ⍺: ⋄ ⍺('reshape_ibeam'⌶)⍵}
rho←('shape_ibeam'⌶) ⎕AMBIV reshape

⍝ ,: Ravel / Catenate
catenate←{
	(0≡lr←≢⍴⍺)∧(0≡rr←≢⍴⍵):(,⍺)∇,⍵
	(lr≢0)∧(rr≢0)∧1<|lr-rr:⎕SIGNAL 4
	fx←lr⌈rr
	lr≡0:(s⍴⍺⊣s[fx]←1⊣s←⍴⍵)∇⍵
	rr≡0:⍺∇s⍴⍵⊣s[fx]←1⊣s←⍴⍺
	(pr↑⍴⍺)∨.≠(pr←(lr⌊rr)-lr=rr)↑⍴⍵:⎕SIGNAL 5
	lc←≢,⍺ ⋄ rc←≢,⍵ ⋄ s←⍴⍵ ⋄ s[fx]←(fx⌷⍴⍵)+fx⌷⍴⍺ ⋄ z←(c←×⌿s)⍴0
	z[i+(fx⌷⍴⍵)×⌊(i←⍳lc)÷fx⌷⍴⍺]←,⍺
	z[i+(fx⌷⍴⍺)×1+⌊(i←⍳rc)÷fx⌷⍴⍵]←,⍵
	s⍴z
}
cat←('ravel_ibeam'⌶) ⎕AMBIV catenate

⍝ ≡: Depth / Same
depth←{
	is_simple ⍵:{0≡≢⍴⍵:0 ⋄ 1}⍵
	⎕SIGNAL 16
}
eqv←depth ⎕AMBIV ('same_ibeam'⌶)

⍝ ≢: Tally / Not Same
nqv←{⊃⍴⍵} ⎕AMBIV ('nqv_ibeam'⌶)

⍝ ⌷: Materialize | Squad indexing
materialize←{⍵}
index←{idx←⊃⍺ ⋄ rnk←≢⍴idx
	0≡is_integer idx:⎕SIGNAL 11
	{0≡⍵:0 ⋄ 1≡⍵:0 ⋄ 1}rnk:⎕SIGNAL 16
	(⍴idx)⍴idx('index_ibeam'⌶)⍵
}
sqd←materialize ⎕AMBIV index

⍝ ⍳: Index Generate | Index Of
index_gen←{
	chk_valid_shape ⍵: 
	0≡≢,⍵:⊂⍬
	(,0)≡,⍵:⍬
	1≡≢,⍵:'index_gen_vec'⌶ ⍵
	⎕SIGNAL 16
}
index_of←{⎕SIGNAL 16}
iot←index_gen ⎕AMBIV index_of

⍝ ⊃: First / Pick
dis←('disclose_ibeam'⌶) ⎕AMBIV {⎕SIGNAL 16}

⍝ ⊂: Enclose / Partition
enclose←{
	0≡≡⍵:⍵
	'enclose_ibeam'⌶ ⍵
}
par←enclose ⎕AMBIV {⎕SIGNAL 16}

⍝ +: Conjugate | Addition
conjugate←{1289≡⎕DR squeeze ⍵:'conjugate_vec'⌶ ⍵ ⋄ ⍵}
add←conjugate ⎕AMBIV ('add_vec_ibeam'⌶ numeric scalar)

⍝ -: Negate | Subtract
sub←{0-⍵} ⎕AMBIV ('sub_vec_ibeam'⌶ numeric scalar)

⍝ ×: Sign | Multiply
sign←{
	1289=⎕DR ⍵:⍵÷|⍵
	(-⍵<0)+0<⍵
}
mul←sign ⎕AMBIV ('mul_vec_ibeam'⌶ numeric scalar)

⍝ ÷: Reciprocal | Divide
div←{1÷⍵} ⎕AMBIV ('div_vec_ibeam'⌶ numeric scalar)

⍝ |: Absolute Value | Residue
absolute←{
	0≡is_numeric ⍵:⎕SIGNAL 11
	1289≡⎕DR ⍵:⎕SIGNAL 11
	'abs_ibeam'⌶ ⍵
}
residue←{⍵-⍺×⌊⍵÷⍺+0=⍺}
res←absolute numeric ⎕AMBIV residue

⍝ ⌊: Floor | Minimum
floor_array←{
	is_integer ⍵:⍵
	1289=⎕DR ⍵:⎕SIGNAL 16
	'floor_vec_ibeam'⌶ ⍵
}
min←floor_array numeric ⎕AMBIV ('min_vec_ibeam'⌶ numeric scalar)

⍝ ⌈: Ceiling | Maximum
ceil_array←{
	is_integer ⍵:⍵
	1289=⎕DR ⍵:-⌊-⍵
	'ceil_vec_ibeam'⌶ ⍵
}
max←ceil_array numeric ⎕AMBIV ('max_vec_ibeam'⌶ numeric scalar)

⍝ *: Exponent | Power
exp←'exp_vec_ibeam'⌶ numeric ⎕AMBIV ('pow_vec_ibeam'⌶ numeric scalar)

⍝ ⍟: Natural Logarithm | Logarithm
log←'nlg_vec_ibeam'⌶ numeric ⎕AMBIV ('log_vec_ibeam'⌶ numeric scalar)

⍝ ○: Pi Times | Trigonometric Functions
pitimes←{3.14159265358979323846×⍵}
trig←{
	0≠≢⍺:⍺ ∇¨⍵
	0=⍺:(1-⍵*2)*.5
	1=⍺:'sin_vec_ibeam'⌶ ⍵
	¯1=⍺:'arcsin_vec_ibeam'⌶ ⍵
	2=⍺:'cos_vec_ibeam'⌶ ⍵
	¯2=⍺:'arccos_vec_ibeam'⌶ ⍵
	3=⍺:'tan_vec_ibeam'⌶ ⍵
	¯3=⍺:'arctan_vec_ibeam'⌶ ⍵
	4=⍺:(1+⍵*2)*.5
	¯4=⍺:(⍵+1)×((⍵-1)÷⍵+1)*0.5
	5=⍺:'sinh_vec_ibeam'⌶ ⍵
	¯5=⍺:'arcsinh_vec_ibeam'⌶ ⍵
	6=⍺:'cosh_vec_ibeam'⌶ ⍵
	¯6=⍺:'arccosh_vec_ibeam'⌶ ⍵
	7=⍺:'tanh_vec_ibeam'⌶ ⍵
	¯7=⍺:'arctanh_vec_ibeam'⌶ ⍵
	8=⍺:(-1+⍵*2)*0.5
	¯8=⍺:-8○⍵
	9=⍺:{1289≡⎕DR ⍵:'realpart_vec_ibeam'⌶ ⍵ ⋄ ⍵}⍵
	¯9=⍺:⍵
	10=⍺:|⍵
	¯10=⍺:+⍵
	11=⍺:{1289≡⎕DR ⍵:'imagpart_vec_ibeam'⌶ ⍵ ⋄ (⍴⍵)⍴0}⍵
	¯11=⍺:⍵×0J1
	12=⍺:'PHASE(⍵) NOT IMPLEMENTED'⎕SIGNAL 16
	¯12=⍺:*⍵×0J1
	⎕SIGNAL 11
}
cir←pitimes numeric ⎕AMBIV (trig numeric scalar)

⍝ !: Factorial | Binomial
binomial←{
	∨⌿,(⍵=⌊⍵)∧(⍵<0)∧⍺≠⌊⍺:⎕SIGNAL 11
	(!⍵)÷(!⍺)×!⍵-⍺
}
fac←'factorial_vec_ibeam'⌶ numeric ⎕AMBIV (binomial numeric scalar)

⍝ ~: Not | Without
notscl←{is_bool ⍵:'not_vec_ibeam'⌶ ⍵ ⋄ ⎕SIGNAL 11}
without←{⎕SIGNAL 16}
not←notscl ⎕AMBIV without

⍝ ∧: And
logand←{
	⍺ both_bool ⍵:⍺('and_vec_ibeam'⌶)⍵
	⍺×⍵÷z+0=z←⍺∨⍵
}
and←{⎕SIGNAL 2}⎕AMBIV (logand numeric scalar)

⍝ ∨: Or
logor←{
	⍺ both_bool ⍵:⍺('lor_vec_ibeam'⌶)⍵
	(is_bool ⍺)∧is_integer ⍵:⍺+(~⍺)×|⍵
	(is_bool ⍵)∧is_integer ⍺:⍵+(~⍵)×|⍺
	⍺ both_integer ⍵:(|⍺){~∨⌿m←⍵≠0:⍺ ⋄ ((⍺×~m)+m×⍵)∇(⍵×~m)+m×⍵|⍺}|⍵
	(|⍺){~∨⌿m←⍵>1e¯12:⍺ ⋄ ((⍺×~m)+m×⍵)∇(⍵×~m)+m×⍵|⍺}|⍵
}
lor←{⎕SIGNAL 2}⎕AMBIV (logor numeric scalar)

⍝ ⍲: Not and
nan←{⎕SIGNAL 2}⎕AMBIV{⍺ both_bool ⍵:~⍺∧⍵ ⋄ ⎕SIGNAL 11}

⍝ ⍱: Not Or
nor←{⎕SIGNAL 2}⎕AMBIV{⍺ both_bool ⍵:~⍺∨⍵ ⋄ ⎕SIGNAL 11}

⍝ <: Less than
lessthan←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('lth_vec_ibeam'⌶)⍵
}
lth←{⎕SIGNAL 2} ⎕AMBIV (lessthan numeric scalar)

⍝ ≤: Less than or equal
lesseql←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('lte_vec_ibeam'⌶)⍵
}
lte←{⎕SIGNAL 2}⎕AMBIV (lesseql numeric scalar)

⍝ =: Equal
eql_vec←'eql_vec_ibeam'⌶
equal←{
	⍺ both_numeric ⍵:⍺ eql_vec ⍵
	⍺ both_char ⍵:⍺ eql_vec ⍵
	0
}
eql←{⎕SIGNAL 2}⎕AMBIV (equal scalar)

⍝ ≥: Greater than or equal
greatereql←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('gte_vec_ibeam'⌶)⍵
}
gte←{⎕SIGNAL 2}⎕AMBIV (greatereql numeric scalar)

⍝ >: Greater than
greaterthan←{
	1289≡⎕DR ⍵:⎕SIGNAL 11 ⋄ 1289≡⎕DR ⍺:⎕SIGNAL 11
	⍺('gth_vec_ibeam'⌶)⍵
}
gth←{⎕SIGNAL 2}⎕AMBIV (greaterthan numeric scalar)

⍝ ≠: Not Equal | First Occurrence
firstocc←{⎕SIGNAL 16}
neq_vec←'neq_vec_ibeam'⌶
noteq←{
	⍺ both_numeric ⍵:⍺ neq_vec ⍵
	⍺ both_char ⍵:⍺ neq_vec ⍵
	1
}
neq←firstocc ⎕AMBIV (noteq scalar)

⍝ ↑: Mix | Take
mix←{⎕SIGNAL 16}
take←{(⊂⍳⍺)⌷⍵}
tke←mix ⎕AMBIV take

⍝ ↓: Split | Drop
split←{⎕SIGNAL 16}
drop←{(⊂⍺+⍳(≢⍵)-⍺)⌷⍵}
drp←split ⎕AMBIV drop

⍝ ⌽: Reverse Last | Rotate Last
reverse_last←{(⊂(≢⍵)-1+⍳≢⍵)⌷⍵}
rotate_last←{⎕SIGNAL 16}
rot←reverse_last ⎕AMBIV rotate_last

⍝ ¨: Each
map_monadic←{⎕SIGNAL 16}
map_dyadic←{⍺ chk_scl ⍵: ⋄ s←⍺ max_shp ⍵ ⋄ s⍴⍺ ⍺⍺ ⎕VEACH ⍵}
map←{⍺ ⍺⍺ map_dyadic ⍵}

⍝ ⌿: Reduce First | N-wise Reduce First
reduce←{
	0≡≢⍴⍵:∇,⍵ ⋄ s←⍴⍵ ⋄ c←⊃s ⋄ zs←1↓s
	0≡c:⎕SIGNAL 16 ⋄ 1≡c:zs⍴⍵ ⋄ op←⍺⍺¨
	z⊣(⌽⍳c-1){⍬⊣z⊢←(⍺⌷⍵)op z}¨⊂⍵⊣z←(c-1)⌷⍵
}
nwreduce←{op←⍺⍺¨
	0≡is_integer ⍺:⎕SIGNAL 11 ⋄ rc←1+≢⍵ ⋄ rc<⍺:⎕SIGNAL 5 ⋄ rc-←|⍺
	z⊣(⌽⍳⍺-1){z⊢←((⊂⍺+⍳rc)⌷⍵)op z}¨⊂⍵⊣z←(⍺-1)↓⍵
}
rdf←{⍺←⊢ ⋄ 1<≢⍴⍺⊣0:⎕SIGNAL 4 ⋄ 1≢≢,⍺⊣0:⎕SIGNAL 5
	⍬≡⍺⊣⍬:⍺⍺ reduce ⍵ ⋄ ⍺(⍺⍺ nwreduce)⍵
}

⍝ ⌿⌿: Replicate First
rpf←{⎕SIGNAL 16}

⍝ .: Inner Product
dot←{⍺⍺⌿⍺ ⍵⍵¨ ⍵}

⍝ ⍣: Power Operator
pow←{0≡⍵⍵:⍵ ⋄ ⍺←⊢ ⋄ ⍺ ⍺⍺∇∇(⍵⍵-1)⊢⍺ ⍺⍺ ⍵}

⍝ ∘: Compose Operator
jot←{
	type←⎕NC'⍺' '⍺⍺' '⍵⍵'
	type≡2 3 3:⍺ ⍺⍺ ⍵⍵ ⍵
	type≡0 3 3:⍺⍺ ⍵⍵ ⍵
	type≡0 2 3:⍺⍺ ⍵⍵ ⍵
	type≡0 3 2:⍵ ⍺⍺ ⍵⍵
	⎕SIGNAL 2
}

:EndNamespace
