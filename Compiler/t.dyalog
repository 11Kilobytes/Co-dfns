:Namespace T
  (⎕IO ⎕ML ⎕WX)←0 1 3
  A←##.A ⋄ d←A.d ⋄ t←A.t ⋄ k←A.k ⋄ n←A.n ⋄ r←A.r ⋄ s←A.s ⋄ v←A.v ⋄ e←A.e
  Am←A.Am ⋄ Em←A.Em ⋄ Fm←A.Fm ⋄ Fs←A.Fs ⋄ Nm←A.Nm ⋄ Om←A.Om
  tt←{fd ff if vc fs av va ce fe da lc lf du df rd rn ⍵}
  rn←⊢,∘↓(1+d)↑⍤¯1(+⍀d∘.=∘⍳1+(⌈/0,d))
  rd←⊢,(+/↑∘r∧.(=∨0=⊢)∘⍉∘↑∘r Fs)
  df←(~(+\1=d)∊((1=d)∧(Om∨Fm)∧0∊⍨n)(/∘⊢)(+\1=d))(⌿∘⊢)⊢
  prf←((≢↑¯1↓(0≠⊢)(/∘⊢)⊢)⍤1↑∘r)⊢
  dua←(Fm∨↓∘prf∊r∘Fs)(⊣(⍀∘⊢)(⊣(⌿∘⊢)0∊⍨n)(0,1↓(¯1⌽⊣)∧⊢=¯1⌽⊢)⊣(⌿∘⊢)d)⊢
  du←(~dua∨(∨/(prf∧.(=∨0=⊢)∘⍉dua(⌿∘⊢)prf)∧↑∘r∧.≥∘⍉dua(⌿∘⊢)↑∘r×0=prf))(⌿∘⊢)⊢
  enc←⊂⊣,∘⊃((⊣,'_',⊢)/(⊂''),(⍕¨(0≠⊢)(/∘⊢)⊢))
  blg←{⍺←⊢ ⋄ ⍺((prf(⌈/(⍳∘≢⊢)×⍤1(1↓⊣)∧.(=∨0=⊢)∘⍉⊢)⍺⍺(⌿∘↑)r)⌷⍤0 2 ⍺⍺(⌿∘⊢)⍵⍵)⍵}
  lfv←⍉∘⍪(1+⊣),'Vf',('fn'enc 4⊃⊢),4↓⊢
  lfn←('F'≡1⊃⊢)⌷(⊣-⍨∘⊃⊢)((⊂∘⍉∘⍪⊣,1↓⊢),∘⊂(⊣,'Of',3↓⊢)⍪lfv)⊢
  lfh←(1<(+/⊣))⊃(⊂0↑⊢),∘⊂∘⍉∘⍪1'F'1,('fn'enc⊣),(⊂⊣),5↓∘,1↑⊢
  lf←(1↑⊢)⍪∘⊃(⍪/(1,1↓Fm)blg(↑r)(⊂lfh⍪∘⊃(⍪/((¯2+1=(+/⊣))+∘⊃⊢)lfn⍤¯1⊢))⌸1↓⊢)
  pck←{⍺(⍺⍺⌷⍤¯1⍵⍵,∘⍪⍺⍺(⍀∘⊢)⊣)⍵}
  lch←⊣((1+Nm),t,'c',Am pck n,r,∘⍪s)(Nm∨Am∧1⌽Nm)(⌿∘⊢)⊢
  lcr←d,(↑(↓'V','a',∘⍪⊣)(Nm∧¯1⌽Am)pck(↓t,k,∘⍪n)⊢),r,∘⍪s
  lc←((⊂'lc'),∘⍕¨∘⍳(+/Am∧1⌽Nm))((1↑⊢)⍪lch⍪1↓((¯1⌽Am)∨∘~Nm)(⌿∘⊢)lcr)⊢
  da←((0∊⍨n)∧Am∨Om∧'f'∊⍨k)((~⊣)(⌿∘⊢)(d-¯1⌽⊣),1↓[1]⊢)⊢
  fen←(0≡∘⊃n)⌷n,'fe'enc∘⊃r
  fer←(d,'V',('af'⊃⍨'O'≡∘⊃t),fen,4↓⊢)⍤1
  fev←(((3↑⊢),fen,4↓⊢)⊣)⍪(Am∨Em∨Om)⌷⍤¯1⊢,[0.5]fer
  fee←⍪/(⌽(1,1↓Am∨Em∨Om)blg⊢((⊂(d-(⊃d)-2⌊∘⊃d),1↓[1]⊢)fev)⌸1↓⊢)
  fe←(⊃⍪/)(+\Fm)(⍪/(⊂1↑⊢),(1↓(+\d=1+∘⊃⊢))fee⌸1↓⊢)⌸⊢
  ce←(+\'AFOE'∊⍨t)(,(1↑⊢),∘⊂∘n 1↓⊢)⌸⊢
  val←(n⍳∘∪n),¨⊢(⊢+(≢⊣)×0=⊢)(⌈/(⍳≢)×⍤1(∪n)∘.((⊂⊣)∊⊢)v)
  vag←∧∘~∘(∘.=⍨∘⍳≢)⍨(∘.(((1⌷⊢)>0⌷⊣)∧(0⌷⊢)<1⌷⊣)⍨val)
  vae←(∪n)(⊣,⍤0⊣(⌷⍨⍤1 0)∘⊃((⊢,(⊃(⍳∘≢⊣)~((≢⊢)↑⊣)(/∘⊢)⊢))/∘⌽(⊂⍬),∘↓⊢))vag
  var←((1⌷∘⍉⊣),⊢)⌷⍨(0⌷∘⍉⊣)⍳⊢
  vaf←(vae(Em∨Am)(⌿∘⊢)⊢)(d,t,k,(⊣var⍤2 0n),r,s,∘⍪(⊂⊣)var⍤2 0¨v)⊢
  va←((⊃⍪/)(1↑⊢),(vaf¨1↓⊢))(1,1↓Fm)⊂[0]⊢
  ⍝ ltf←
  ⍝ lt←(⊃∘⊃∘(ltf/)∘⌽(⊂(0 7⍴⍬)⍬ ⍬),⊢)(1,1↓Fm))⊂[0]⊢
  avb←{(((,¨'⍺⍵')↑⍨1↓⍴)⍪⊢)⍺⌷⍨⍤2 0⊢⍺⍺⍳⍺⍺∩⍨(↓(⌽1+∘⍳0⍳⍨⊢)((≢⊢)↑↑)⍤0 1⊢)⊃r ⍵}
  avh←{⊂⍵,(n⍵)((⍺⍺(⍵⍵ avb)⍵){¯1 0+[0](⍴⍺⍺)⊤(,⍺⍺)⍳(⊂⍺),⍵})¨v⍵}
  av←(⊃⍪/)(+\Fm){⍺((⍺((∪n)(Em∨Am)(⌿∘⊢)⊢)⌸⍵)avh(r(1↑⍵)⍪Fs ⍵))⌸⍵}⊢
  fsp←,¨'+-×÷|⌊⌈*⍟○!∧∨⍱⍲<≤=≥>≠' ⋄ fsh←⍉⍪2'S'0 ⍬ ⍬ 0 ⍬ ⍬
  fss←Em∧('md'∊⍨k)∧fsp∊⍨(↑v)⌷⍤¯1⍨'md'⍳k ⋄ fsg←1,2≠/fss
  fse←(⊃⍪/)(fsg(/∘⊢)fss∧fsg)(⊣⊃(⊂⊢),(⊂fsh⍪(1+d),1↓[1]⊢))¨fsg⊂[0]⊢
  fs←(⊃⍪/)((((~Em∨Am)(⌿∘⊢)⊢)⍪(fse(Em∨Am)(⌿∘⊢)⊢))¨(1,1↓Fm)⊂[0]⊢)
  vcs←≢∘∪∘n(Am∨Em)(⌿∘⊢)⊢
  vc←(⊃⍪/)(((,1↑⊢)(((6↑⊣),(⊂⍬),⍨vcs)⍪⊢)1↓⊢)¨(1,1↓Fm)⊂[0]⊢)
  ifn←1 'F' 0 'Init' ⍬ 0,(⊂⍬),⍨⊢
  if←(1↑⊢)⍪((1=d)∧Em∨Am)((((ifn≢∘∪∘n)⍪A.up)⊣(⌿∘⊢)⊢)⍪1↓(~⊣)(⌿∘⊢)⊢)⊢
  fft←(,1↑⊢)(1 'Z',(2↓¯2↓⊣),(n⊢),(⊂0⌷∘⍉∘⊃∘e⊢))(¯1↑⊢)
  ff←((⊃⍪/)(1↑⊢),(((1↑⊢)⍪(((¯1+d),1↓[1]⊢)1↓⊢)⍪fft)¨1↓⊢))(1,1↓Fm)⊂[0]⊢
  fd←(1↑⊢)⍪((1,'Fd',3↓⊢)⍤1Fm(⌿∘⊢)⊢)⍪1↓⊢
:EndNamespace
