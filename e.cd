⍝ A  B  E  F  G  L  M  N  O  P  V  Z
⍝ 0  1  2  3  4  5  6  7  8  9 10 11
tt←{d t k n←⍵ ⋄ I←{(⊂⍵)⌷⍺} ⋄ gf←{p I@{3≠t[⍵]}⍣≡p[⍵]}
 _←2{0⊣l[⍵[i]]←⍵[¯1+i←⍸0,2=⌿i]⊣p[⍵]←⍺[i←⍺⍸⍵]}⌿⎕WA⊢⊢∘⊂⌸d⊣p←l←⍳≢d
 i←⍸p=⍳s←≢p ⋄ l,←⍸(p≠⍳≢p)∧(p[p]=p)∧~l(⊢∊⊣(⌿⍨)≠)⍳≢l
 p,←i ⋄ t k n,←2 0 0⍴⍨¨⊂≢i ⋄ j←⍸(t=1)∧(k=1)∧p[p]=p ⋄ g←p[j]⊢∘⊂⌸v←s+(≢i)+⍳≢j
 p,←(≢¨g)⌿s+⍳≢i ⋄ l,←∊(⊃,¯1↓⊢)¨g ⋄ t k,←10 1⍴⍨¨≢j ⋄ n,←n[j] ⋄ t[⍸p=⍳≢p]←3
 i←(⍸(l=⍳s)∧p[p]=p),j←⍸(t=3)∧p≠⍳s←≢p ⋄ l←l×(~m)+(m←x<≢j)×x←j⍳l
 p l(⊣,I)←⊂j ⋄ t k,←10 1⍴⍨¨≢j ⋄ n,←j
 p[j]←(-≢j)↑p∆←p I⍣≡i ⋄ _←p∆{l[⍵]←¯1⌽(⊃,⊢)1↓⍵}⌸i
 ⎕←(2 3 4 lb3(⊂p l t k n),#.(exports symbols))(dwh pp3) p l
 p l t k n
 bi←⍸1=t ⋄ bn←n[bi] ⋄ bx←x[bi] ⋄ bv←{⍺[bi⍳⍵]}@{1=t[⍵]}⍨⍣≡{⍵[⍋p[⍵]]}⍸1=t[p]
 p←bi(⊢-1+⍸)p I@{1=t[⍵]}⍣≡p[nb←⍸t≠1]
 l←bi(⊢-1+⍸)nb I⍨{bv[bi⍳⍵]}@{1=t[⍵]}l[bi[i]]@(bv[i])⊢l⊣i←⍸bi≠l[bi]
 t k n x I¨←⊂nb ⋄ bx,←x⌿⍨3=t ⋄ bn bv bx←(⊂⍋bx)I⍨¨(≢bx)↑¨bn(bi(⊢-1+⍸)bv)bx
 _←{lv←{⍸⍵∧(t=10)∧n≥0} ⋄ fv←{⍸⍵∧(t=10)∧n<¯4}
  n[i](⊢+⊣×0=⊢)←bv[n[i]{⍵-⊃⍺ 0∧.≠⊂bn[⍵]}⍣≡bx⍸x[i←fv 1]]
  _←{i⊣t[i]←2⊣k[i]←6⊣i←⍸(t[p]=2)∧t=8}⍣{0=≢⍺}⍬
  _←{_←{⍵⌿10=t[n[⍵]]⊣n[⍵]←n[n[⍵]]}⍣{0=≢⍺}⍸10=t[n[⍵]]
   i←(t[p[i]]=2)∧t[n[i]]=8)⌿i←⍵⌿⍨0≤n[⍵] ⋄ m←n[i]∘.=p ⋄ s←≢p
   x,←x[p,←p∆←i⌿⍨+/m] ⋄ n,←c←s|⍸,m ⋄ t,←10⍴⍨≢c ⋄ l,←s+(p∆,⍪c)⍳p∆,⍪l[c]
   k,←(∊∘3 8 9∨k[c]∧10=⊢)t[c] ⋄ t[i]←2 ⋄ k[i]←6 ⋄ n[i]←0 ⋄ s+⍳≢c}⍣{0=≢⍺}lv 1
  _←{t[⍵]←9⊣k[⍵]←0⊣n[⍵]←n[n[⍵]]}⍸t[n[lv 1]]=9
  {t[⍵]←2⊣k[⍵]←5}lv(t[p]=2)∧k=1 ⋄ e5←⍸(t=2)∧k=5 ⋄ m←n[e5]∘.=gf⊢i←fv 1
  c←i[(≢i)|⍸,m] ⋄ s←≢p ⋄ x,←x[p,←e5⌿⍨+/m] ⋄ l,←s+⍳≢c ⋄ t,←10⍴⍨≢c
  k,←k[c] ⋄ n,←n[c] ⋄ s+⍳≢c}⍣{0=≢⍺}⍬
 l[gr]←gr←⍸(l[l]=⍳≢l)∧gm←4=t[p] ⋄ n[p[gv]]←n[gv←⍸(10=t)∧gk←gm∧l=⍳≢l]
 p[ge]←p[pg←p[ge←⍸gk∧2=t]] ⋄ l[ge]←l[pg] ⋄ l[pg]←n[pg]←ge
 gn←⍸~gk∧10=t ⋄ p l n←(⊢-1+gv⍸⊢)¨(p[gn])(l[gn])(n[gn])xv ⋄ t←t[gn] ⋄ k←k[gn]
 ⍝ Flatten expressions
 ⍝ Label jumps
 ⍝ Inline functions
 ⍝ Propagate constants
 ⍝ Fold constants
 ⍝ Dead, useless code elimination
 ⍝ Allocate frames
 ⍝ Create Init function
 ⍝ Declare functions
 ⍝ Sort AST
 ⍝ Flatten AST
 p l t k n x}
